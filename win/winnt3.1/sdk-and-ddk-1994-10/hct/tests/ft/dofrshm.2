@rem ************************************************************************
@rem Copyright (c) 1991 Microsoft Corporation
@rem
@rem Module Name:
@rem   dofrshm.2   
@rem
@rem Abstract:
@rem   This is the command script responsible for the fresh mirror test.
@rem   This script performs the following:
@rem     - runs smash.exe to enable the autologon featuren
@rem     - calls scendat.cmd to get the name of the logfile
@rem     - calls getdat.cmd to get the ft componet info (drive letters) 
@rem     - performs some tests
@rem     - copies the next do<testname>.* script to donext.
@rem     - runs the delete.mst script to remove the the broken mirror.
@rem       The system will reboot with donext copied to do.cmd.
@rem
@rem Author:
@rem   Kenieth Peery (t-kenpe) 12-9-92
@rem
@rem Revision History:
@rem *************************************************************************

call doinit

@rem
@rem If we orphaned during writes, then only check the data on the one
@rem that was not orphaned.  orphan.cmd should be laying around from
@rem previous boot.
@rem
@rem Note: There is a problem where ftstate -o set which_member based
@rem       on the order in the registry, and create.dat, in general,
@rem       makes the primary/shadow in the oppisitte direction.  If
@rem       this assumption is not always true, then the below code will
@rem       not work.  This should be made more bullet proof some day.
@rem 

if not "%_options1%" == "orphan_writes" goto no_orph_writes
call orphaned.cmd
if "%_which_member%" == "0" set _skip2=1
if "%_which_member%" == "1" set _skip1=1
:no_orph_writes

@rem
@rem Run endtomd and chkdsk on both the primary and shadow.
@rem

if not "%_skip1%" == "" goto skipit1
call doendmd %driveLetter1%:\foo.bin %logfile% 
call dochkdsk %driveLetter1% %logfile% 

:skipit1

if not "%_skip2%" == "" goto skipit2
call doendmd %driveLetter2%:\foo.bin %logfile% 
call dochkdsk %driveLetter2% %logfile% 

:skipit2

@rem
@rem Compare the files on the primary and shadow.  If orphaned writes
@rem can't do this either.
@rem

if "%_options1%" == "orphan_writes" goto no_comp
call docomp %driveLetter1%:\foo.bin %driveLetter2%:\foo.bin %logfile% 
:no_comp

@rem
@rem Delete the partitions and move to next variation
@rem

echo.                                                 >> %logfile%
echo TRACE: About to delete the partitions and reboot >> %logfile%
echo.                                                 >> %logfile%

copy do.0 donext
testdrvr delete.mst /run 
