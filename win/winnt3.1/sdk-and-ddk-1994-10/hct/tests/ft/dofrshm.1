@rem ************************************************************************
@rem Copyright (c) 1991 Microsoft Corporation
@rem
@rem Module Name:
@rem   dofrshm.1   
@rem
@rem Abstract:
@rem   This is the command script responsible for the fresh mirror test.
@rem   This script performs the following:
@rem     - runs smash.exe to enable the autologon featuren
@rem     - calls getdat.cmd to get the ft componet info  
@rem     - calls scendat.cmd to get the name of the logfile
@rem     - calls formatft.cmd to format the previously created ft componet
@rem     - performs some tests
@rem     - copies the next do<testname>.* script to donext.
@rem     - runs the break.mst script to break the mirror for the next stage of
@rem       tests.  The system will reboot with donext copied to do.cmd.
@rem
@rem Author:
@rem   Kenieth Peery (t-kenpe) 12-9-92
@rem
@rem Revision History:
@rem *************************************************************************

call doinit

@rem
@rem Format the mirror, write file, read file, etc...
@rem

call doformat %label% %fileSystem% %driveLetter1% %logfile%

if "%_options1%" == "badsectors" call dobadsec %driveLetter1% 128 %logfile%

if "%_options1%" == "orphan_writes" call doorph %driveLetter1% %sizeMB% 0.4 %logfile%

call dosetmap %driveLetter1%:\foo.bin %_fill_percent% %logfile%   

if "%_options1%" == "orphan_writes" call dochorph %logfile%
if "%_options1%" == "orphan_writes" call dogetlog %logfile%

call dodir %driveLetter1% %logfile%   

if "%_options1%" == "badsectors" call dobadsec %driveLetter1% 128 %logfile%

if "%_options1%" == "orphan_reads" call doorph %driveLetter1% %sizeMB% 0.2 %logfile%

call doendmd %driveLetter1%:\foo.bin %logfile%   

if "%_options1%" == "orphan_reads" call dochorph %logfile%
if "%_options1%" == "orphan_reads" call dogetlog %logfile%

call dochkdsk %driveLetter1% %logfile%   

@rem
@rem Break the mirror
@rem

echo.                                              >> %logfile%
echo TRACE: About to break the mirror and SHUTDOWN >> %logfile%
echo.                                              >> %logfile%

copy dofrshm.2 donext
testdrvr break.mst  /run
