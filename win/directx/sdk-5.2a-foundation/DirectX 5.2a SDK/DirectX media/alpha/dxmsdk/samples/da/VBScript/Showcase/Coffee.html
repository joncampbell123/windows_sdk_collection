<HTML>
<HEAD>
<TITLE>DirectAnimation SDK, VBScript sample</TITLE>
<SCRIPT LANGUAGE="JScript">
<!--
if (window.top.frames.length!=0 && window.top.frames[0].ShowNoButtons!=null)
    window.top.frames[0].ShowNoButtons();
//-->
</SCRIPT>
</HEAD>
 
<BODY BGCOLOR=WHITE LINK="#0033CC" TOPMARGIN=15 LEFTMARGIN=20>
<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>
<CENTER>
<H1>	Coffee Sprite Animation
</H1>
</CENTER>

<OBJECT ID="DAControl" WIDTH=475 HEIGHT=225 ALIGN=LEFT HSPACE=20
   
   CLASSID="CLSID:B6FFC24C-7E13-11D0-9B47-00C04FC2F51D">
</OBJECT>
<P>
This sample demonstrates the use of sprites, 
montages, and reactivity in DirectAnimation.  
Click on the image to activate the espresso machine.
<HR>

<SCRIPT LANGUAGE="VBScript">
<!--

Set m = DAControl.MeterLibrary
Set steamDurationConst = m.DANumber(7.25)
pi =  3.14159265359

Sub window_onLoad
  'Set the background
  Set imgBackGround = m.importImage("../../../../media/image/clouds_coffee.gif")

  'Create the final montage by layering the espresso machine, the cups and the steam
  'on top of each other. 
  Set finalMtg = m.UnionMontage(m.UnionMontage(steamMontage(), _ 
  montage()),machineMontage())

  'Create the final image.
  Set finalImage = m.Overlay(finalMtg.Render(),m.Overlay(beans(),imgBackGround))

  'Set the sound.  
  DAControl.Sound = m.Mix(sound().Pan(-1),sound().Pan(1))

  'Display the image.
  DAControl.Image = finalImage

  'Set the background in case of a non-windowless browser (such as IE3).
  DAControl.BackgroundImage = m.SolidColorImage(m.Blue)

  'Start the animation
  DAControl.Start
End Sub

Function sound()
  'This function creates the sound, which starts out as silence, and then
  'changes to steam.mp2 when the image is clicked.
  Set steamSound = m.ImportSound("../../../../media/sound/steam.mp2").Sound

  Set s0 = CreateObject("DirectAnimation.DASound")
    s0.Init(m.Until(m.Silence,m.leftButtonDown, _ 
      m.Until(steamSound.Gain(0.85),m.TimerAnim(steamDurationConst),s0)))

  Set sound = s0
End Function

Function montage()
  'This function creates a montage of five cups that rotate around the espresso
  'machine.  The orbit is constructed by the orbitCup function.
  total = 5
  Set cupImageX = m.EmptyMontage

  For i=0 To total
    Set cupImageX = m.UnionMontage(cupImageX, orbitCup(m.Add(m.Mul(m.DANumber(i), _ 
	  m.Mul(m.DANumber(2),m.Div(m.DANumber(pi),m.DANumber(total)))),m.LocalTime)))
  Next
  Set montage = cupImageX

End Function

Function beans()
  'This function creates the beans you see in the background.  Two images,
  'bean1.gif and bean2.gif are imported, and then moved across the screen
  'while being rotated.
  Set delay = m.DANumber(0.5)
  Set size = m.DANumber(0.5)
  Set initBean1 = m.ImportImage("../../../../media/image/bean1.gif")
  Set initBean2 = m.ImportImage("../../../../media/image/bean2.gif")

  Set image0 = CreateObject("DirectAnimation.DAImage")
  Set image1 = CreateObject("DirectAnimation.DAImage")
  image0.Init(m.Until(initBean1,m.TimerAnim(delay),image1))
  image1.Init(m.Until(initBean2,m.TimerAnim(delay),image0))

  Set bean1 = image0.Transform(m.Scale2UniformAnim(size))
  Set bean2 = image1.Transform(m.Scale2UniformAnim(size))

  Set beans = m.Overlay(bean1.Transform(m.Translate2(-0.01,-0.01)), _ 
    bean2.Transform(m.Translate2(0.01,0)))

  Set rain = beans.Tile()

  Set motion = m.Mul(m.LocalTime,m.Mul(m.DANumber(2),m.Div(m.DANumber(0.03),m.DANumber(4))))

  Set beans = rain.Transform(m.Translate2Anim(m.Neg(motion), m.Neg(motion)))
End Function

Function machineMontage()
  'This function displays the espresso machine.
  Set espreso1 = m.ImportImage("../../../../media/image/espreso1.gif")
  Set espreso2 = m.ImportImage("../../../../media/image/espreso2.gif")

  Set image5 = CreateObject("DirectAnimation.DAImage")
    image5.Init(m.Until(espreso1,m.LeftButtonDown,m.Until(espreso2, _
      m.TimerAnim(steamDurationConst),image5)))

  Set machineMontage = m.ImageMontage(image5,0)
End Function

Function steamMontage()
  'This function displays the steam you see when you click on the image.
  Dim steamImages(4)
  Set steamImages(0) = m.ImportImage("../../../../media/image/steam_1.gif")
  Set steamImages(1) = m.ImportImage("../../../../media/image/steam_2.gif")
  Set steamImages(2) = m.ImportImage("../../../../media/image/steam_3.gif")
  Set steamImages(3) = m.ImportImage("../../../../media/image/steam_4.gif")
  Set steamImages(4) = m.ImportImage("../../../../media/image/steam_5.gif")

  Set steamLen = m.DANumber(4)

  Set condition = m.GT(m.Add(m.Div(m.Mul(m.LocalTime,steamLen), _ 
    steamDurationConst),m.DANumber(1)),steamLen)

  Set result2 = m.Add(m.Div(m.Mul(m.LocalTime,steamLen), _ 
    steamDurationConst),m.DANumber(1))

  Set index = m.Cond(condition,steamLen,result2)

  Set a = m.DAArray(steamImages)

  Set s0 = CreateObject("DirectAnimation.DAImage")
    s0.Init(m.Until(m.EmptyImage,m.LeftButtonDown,m.Until(a.NthAnim(index), _
      m.TimerAnim(steamDurationConst),s0)))

  Set image1 = s0.Transform(m.Translate2(-0.0085,0.0020))

  Set steamMontage = m.imageMontage(image1,-0.0001)
  End Function

Function orbitCup(angle)
  Set pos = m.Point3(0,.05,0)
  Set pos = pos.Transform(m.Compose3(m.Rotate3Anim(m.XVector3,m.Mul(m.DANumber(7), _ 
    m.Div(m.DANumber(pi),m.DANumber(16)))),m.Rotate3Anim(m.ZVector3, angle)))

  Set cupAngle = m.LocalTime

  Set imageXX = cupImage(cupAngle).Transform(m.Compose2(m.Translate2Anim(pos.X,pos.Y), _ 
    m.Scale2UniformAnim(m.Sub(m.DANumber(1),m.Mul(m.Abs(m.Cos(m.Div(angle,m.DANumber(2)))), _
	  m.DANumber(0.5))))))

  Set orbitCup = m.ImageMontageAnim(imageXX,m.Neg(pos.Z))
End Function

Function cupImage(cupAngle)
  Dim cupImages
  cupImages = Array(m.ImportImage("../../../../media/image/cup1.gif"), _ 
    m.ImportImage("../../../../media/image/cup2.gif"),m.ImportImage("../../../../media/image/cup3.gif"), _
      m.ImportImage("../../../../media/image/cup4.gif"),m.ImportImage("../../../../media/image/cup5.gif"), _
	    m.ImportImage("../../../../media/image/cup6.gif"),m.ImportImage("../../../../media/image/cup7.gif"), _
	      m.ImportImage("../../../../media/image/cup8.gif"))

  Set number = m.DANumber(7)
  Set index  = m.Add(m.Mod(m.Mul(number,m.Div(cupAngle,m.Mul(m.DANumber(2),m.DANumber(pi)))), _ 
    number),m.DANumber(1))

  Set a = m.DAArray(cupImages)

  Set cupImage = a.NthAnim(index)
End Function

-->
</SCRIPT>
</FONT>
</BODY>
</HTML>
