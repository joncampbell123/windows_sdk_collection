<HTML>
<HEAD>
<TITLE>DirectAnimation SDK, VBScript sample</TITLE>
<SCRIPT LANGUAGE="JScript">
<!--
if (window.top.frames.length!=0 && window.top.frames[0].ShowNoButtons!=null)
    window.top.frames[0].ShowNoButtons();
//-->
</SCRIPT>
</HEAD>
 
<BODY BGCOLOR=WHITE LINK="#0033CC" TOPMARGIN=15 LEFTMARGIN=20>
<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>
<CENTER>
<H1>	FLYING CUBES SAMPLE
</H1>
</CENTER>
<OBJECT ID="DAControl" WIDTH=800 HEIGHT=300
   CLASSID="CLSID:B6FFC24C-7E13-11D0-9B47-00C04FC2F51D">
</OBJECT>
<BR>
<P> 
Click on the DirectAnimation control to stop the horizontal movement of the animation. 
Click again to resume.
</P>

<SCRIPT LANGUAGE="VBScript">
<!--
Set m = DAControl.MeterLibrary

Sub window_onLoad    
  'Add reactivity.
  Set changingRate = CreateObject("DirectAnimation.DANumber")
    changingRate.Init(m.Until(m.DANumber(0.2), m.LeftButtonDown, _ 
      m.Until(m.DANumber(0), m.LeftButtonDown, changingRate)))
    
  'Import the geometry & images & sound.
  Set geo = m.ImportGeometry("../../../../media/geometry/cube.x")
  Set importSnd = m.ImportSound("../../../../media/midi/circus.mid")
  Set backgroundSnd = importSnd.Sound.RepeatForever()

  Dim IELogos(8)
  
  Set IELogos(0) = m.ImportImage("../../../../media/image/clogo1.gif")
  Set IELogos(1) = m.ImportImage("../../../../media/image/clogo2.gif")
  Set IELogos(2) = m.ImportImage("../../../../media/image/clogo3.gif")
  Set IELogos(3) = m.ImportImage("../../../../media/image/clogo4.gif")
  Set IELogos(4) = m.ImportImage("../../../../media/image/clogo5.gif")
  Set IELogos(5) = m.ImportImage("../../../../media/image/clogo6.gif")
  Set IELogos(6) = m.ImportImage("../../../../media/image/clogo7.gif")
  Set IELogos(7) = m.ImportImage("../../../../media/image/clogo8.gif")
  Set IELogos(8) = m.ImportImage("../../../../media/image/clogo9.gif")

  'Call the Movie(), which cycles through the IELogos images every second.
  Set imageCube = Movie(IELogos)
  
  'Set lights.
  Set pLight  = m.PointLight.Transform(m.Translate3(0,0,4))
  Set dLight = m.DirectionalLight.Transform(m.Rotate3(m.YVector3, 0.5))
    
  'Manipulate the geometry.
  Set geo = geo.Transform(m.Scale3Uniform(0.5)).Texture(imageCube.MapToUnitSquare())

  'Create the four cubes and start them rotating.
  Set srate = m.Mul(m.LocalTime, m.DANumber(9))
  Set cube1 = geo.Transform(m.Compose3(m.Translate3(0, 0, 2), _ 
    m.Rotate3Anim(m.Vector3(0, 1, 0), m.Mul(m.DANumber(0.04), srate))))
  Set cube2 = geo.Transform(m.Compose3(m.Translate3(0, 0, -2), _ 
    m.Rotate3Anim(m.Vector3(0, 1, 1), m.Mul(m.DANumber(0.03), srate))))
  Set cube3 = geo.Transform(m.Compose3(m.Translate3(2, 0, 0), _ 
    m.Rotate3Anim(m.Vector3(0, 0, 1), m.Mul(m.DANumber(0.02), srate))))
  Set cube4 = geo.Transform(m.Compose3(m.Translate3(-2, 0, 0), _ 
    m.Rotate3Anim(m.Vector3(1, 1, 1), m.Mul(m.DANumber(0.03), srate))))
    
  'Do the final prep work on the model.
  Set finalgeo = m.UnionGeometry(cube1, m.UnionGeometry(cube2, m.UnionGeometry(cube3, cube4)))
  Set Camera = m.PerspectiveCamera(5.5,4.5).Transform(m.Scale3(45, 45, 1))
    
  'Make the complete image rotate.
  Set finalgeo = finalgeo.Transform(m.Rotate3Anim(m.YVector3, m.Integral(changingRate)))
  Set finalgeo = m.UnionGeometry(finalgeo, pLight)
    
  'Render and display the image.
  Set rendered_geo = finalgeo.Render(Camera)

  'Set the sound.
  DAControl.Sound = backgroundSnd
  
  'Set the image.
   DAControl.Image = m.Overlay(rendered_geo, m.SolidColorImage(m.White))

  'Set the background in case of a non-windowless browser (such as IE3).
  DAControl.BackgroundImage = m.SolidColorImage(m.Blue)

  'Start the animation.
  DAControl.Start
End Sub

Function Movie(IELogos)
  Set movieArray = m.DAArray(IELogos)
  Set Movie = movieArray.NthAnim(m.Mod(m.localTime,m.DANumber(9))) 
End Function
-->
</SCRIPT>
</FONT>
</BODY>
</HTML>
