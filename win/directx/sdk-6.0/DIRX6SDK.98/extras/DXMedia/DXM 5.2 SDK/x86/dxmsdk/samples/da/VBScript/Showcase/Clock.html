<HTML>
<HEAD>
<TITLE>DirectAnimation SDK, VBScript sample</TITLE>
<SCRIPT LANGUAGE="JScript">
<!--
if (window.top.frames.length!=0 && window.top.frames[0].ShowNoButtons!=null)
    window.top.frames[0].ShowNoButtons();
//-->
</SCRIPT>
</HEAD>	 
 
<BODY BGCOLOR=BLACK LINK="#0033CC" TOPMARGIN=15 LEFTMARGIN=20>
<FONT FACE="Verdana, Arial, Helvetica" COLOR=WHITE SIZE=2>
<CENTER>
<H1>CLOCK SAMPLE</H1>
</CENTER>

<OBJECT ID="DAControl" WIDTH=220 HEIGHT=220 ALIGN=LEFT HSPACE=20
   CLASSID="CLSID:B6FFC24C-7E13-11D0-9B47-00C04FC2F51D">
</OBJECT>

<P>
This sample displays a clock and demonstrates the integration between VBScript and DirectAnimation.  The time
(hours, minutes, and seconds) for the clock is obtained using VBScript functions.  These values are then
used to construct the corresponding clock-hands using the DirectAnimation control.
</P>

<SCRIPT LANGUAGE="VBScript">
<!--
  Set m = DAControl.PixelLibrary
  pi = 3.14159265359

  Sub window_onLoad
    'Get the current time and break it down into hours, minutes, and seconds.
    a = time
    min1 = minute(time)
    hr1 = hour(time)
    sec1 = second(time)
    Set xPos = m.Mul(m.DANumber(150), m.Cos(m.Mul(m.LocalTime,m.DANumber(0.3))))
    Set yPos = m.Mul(m.DANumber(35), m.Cos(m.Mul(m.LocalTime,m.DANumber(0.5))))

    'Create the final clock image.
    Set clock =  m.Overlay(hands(hr1,min1,sec1),face())

    'Display the clock.
    DAControl.Image = clock

    'Set the background in case of a non-windowless browser (such as IE3).
    DAControl.BackgroundImage = m.SolidColorImage(m.Blue)

    'Start the animation.
    DAControl.Start
  End Sub
  
  Function face()
    'Create the background for the clock.
    Set fs1 = m.Font("Verdana",14,m.Yellow).Bold()
	Set lineStyle = m.DefaultLineStyle.Color(m.Black)
    Set fillStyle = m.SolidColorImage(m.ColorRGB(64,64,255))

    Set txtPath1 = m.StringPathAnim(m.DAString("DIRECT"), fs1)
    Set textpcs1 = txtPath1.fill(lineStyle, fillStyle).Transform(m.Translate2(-10,-30))

    Set txtPath2 = m.StringPathAnim(m.DAString("ANIMATION"), fs1)
    Set textpcs2 = txtPath2.fill(lineStyle, fillStyle)

    Set txtPath3 = m.StringPathAnim(m.DAString("TIME"), fs1)
    Set textpcs3 = txtPath3.fill(lineStyle, fillStyle).Transform(m.Translate2(10,30))

    Set fgColor = m.Red
    Set bgColor = m.Blue

    Set bgFill= m.RadialGradientRegularPoly(fgColor,bgColor,50,2)

	Set bgFill = bgFill.Transform(m.Scale2Uniform(0.055))

    Set background = m.Oval(200,200).Fill(m.DefaultLineStyle,bgFill)

    Set text = m.Overlay(textpcs1,m.Overlay(textpcs2,textpcs3))

    'Create the numbers for the clock.
     Set fs2 = m.Font("Verdana",12,m.White).Bold()
     For i = 1 To 12
       Set vec = m.Vector2(82.5,0).Transform(m.Rotate2(-pi/6*(i-3)))
       Set text = m.Overlay(m.StringImage(i,fs2).Transform(m.Compose2(m.Translate2Vector(vec), _
	     m.Scale2Uniform(1.5))),text)
     Next 

    Set text = text.Transform(m.Translate2(1,9))

    'Put the numbers on top of the background.
     Set face = m.Overlay(text,background)

  End Function

  Function hands(hr,min,sec)
    'Create the hour, minute and second hands of the clock.
    Set bvr60 = m.DANumber(60)
    Set secFromMidnight = m.Add(m.DANumber(hr*3600+min*60+sec),m.LocalTime)
    Set secBvr = m.Mod(secFromMidnight,bvr60)
    Set minBvr = m.Mod(m.Div(secFromMidnight,bvr60),bvr60)
    Set hrBvr = m.Mod(m.Div(secFromMidnight,m.DANumber(3600)),m.DANumber(12))
    
	ptsSec = Array( -5, -2, 90, -2, 90, 2, -5, 2 )
    ptsMin = Array(-5, -3, 80, -3, 80, 3, -5, 3 )
    ptsHr = Array(-5, -5, 65, -3, 65, 3, -5, 3 )

    Set temp1 = m.Mul(m.DANumber(-pi/30),m.Sub(secBvr,m.DANumber(15)))
    Set temp2 = m.Mul(m.DANumber(-pi/30),m.Sub(minBvr,m.DANumber(15)))
    Set temp3 = m.Mul(m.DANumber(-pi/6),m.Sub(hrBvr,m.DANumber(3)))

    Set imgSec = m.PolyLine(ptsSec).Fill(m.DefaultLineStyle,m.SolidColorImage(m.White))
    Set imgSec = imgSec.TransForm(m.Rotate2Anim(temp1))

    Set imgMin = m.PolyLine(ptsMin).Fill(m.DefaultLineStyle,m.SolidColorImage(m.Red))
    Set imgMin = imgMin.TransForm(m.Rotate2Anim(temp2))

    Set imgHr = m.PolyLine(ptsHr).Fill(m.DefaultLineStyle,m.SolidColorImage(m.Magenta))
    Set imgHr = imgHr.TransForm(m.Rotate2Anim(temp3))
	
    Set hands = m.Overlay(imgSec,m.Overlay(imgMin,imgHr))

  End Function
-->
</SCRIPT>
</FONT>
</BODY>
</HTML>
