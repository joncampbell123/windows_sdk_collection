<HTML>
<HEAD>       
<TITLE>DirectAnimation SDK, VBScript sample</TITLE>
<SCRIPT LANGUAGE="JScript">
<!--
if (window.top.frames.length!=0 && window.top.frames[0].ShowNoButtons!=null)
    window.top.frames[0].ShowNoButtons();
//-->
</SCRIPT>
</HEAD>
<BODY BGCOLOR=WHITE LINK="#0033CC" TOPMARGIN=15 LEFTMARGIN=20>  
<FONT SIZE=4 FACE="Verdana,Tahoma,Arial">

<CENTER><P><FONT SIZE=6><FONT COLOR=red>D</FONT>ancing <FONT COLOR=red>T</FONT>rapezoids</FONT></P><BR>
DirectAnimation has a powerful, high-level interface for sequencing any behavior. DirectAnimation 
behaviors support methods for specifying their duration, number of repetitions, and for
sequencing two behaviors together so that one will begin when the other ends.  This 
example shows how this can be used to create very complex transformations with a 
minimum amount of scripting.
</CENTER>
</FONT>


<DIV ID=controlDiv>
<OBJECT ID="DAControl"
  STYLE="position:absolute; left:20%; top:0;width:500;height:500" 
  CLASSID="CLSID:B6FFC24C-7E13-11D0-9B47-00C04FC2F51D">
</OBJECT>
</DIV>



<SCRIPT LANGUAGE="VBScript">
<!--
Sub window_onLoad 
  ' The DirectAnimation library.
  Set m = DAControl.MeterLibrary
  
  ' The number of squares.  
  numGeos = 5
  
  ' Let the speed vary over time.
  Set speed = m.Div(m.LocalTime, m.DANumber(2.5))
  
  ' Define transformation variables.
  Set offX = m.Mul(m.DANumber(.04),m.Sin(speed))
  Set offY = m.Mul(m.DANumber(.04),m.Sin(speed))
  Set rotX = m.Mul(speed, m.DANumber(2))
  Set rotY = m.Mul(speed, m.DANumber(2))
  Set rotZ = m.Mul(speed, m.DANumber(2))
  Set lrotX = m.Mul(m.DANumber(.02),speed)
  Set lrotY = m.Mul(m.DANumber(.02),speed)
  Set lrotZ = m.Mul(m.DANumber(.02),speed)
  Set scale = m.DANumber(1)

  ' Create an animated color.
  Set half = m.DANumber(0.5)
  Set hue = m.Abs(m.Sin(m.Div(speed,m.DANumber(2)))) 
  Set clr = m.colorHslAnim(hue, half, half)

  Set xfID = m.IdentityTransform2  
  Set xfsc = m.Scale2UniformAnim(scale)
  
  ' Use the above created variables to construct three dimensional rotations.
  ' Then use the ParallelTransform2 method to cast an orthographic projection
  ' of these rotations onto the X-Y plane.
  Set xflr = m.Compose3(m.Compose3(m.Rotate3Anim(m.xVector3,lrotX), _
    m.Rotate3Anim(m.yVector3,lrotY) ), m.Rotate3Anim(m.zVector3,lrotZ) ).ParallelTransform2()
  Set xfwt = m.Compose3(m.Compose3(m.Rotate3Anim(m.xVector3,rotX), _
    m.Rotate3Anim(m.yVector3,rotY) ), m.Rotate3Anim(m.zVector3,rotZ) ).ParallelTransform2()

  ' Create an two dimensional translation.
  Set xflt = m.Translate2Anim(offX, offY)
  
  ' Combine the translation with the rotations.
  Set xf = m.Compose2(xfwt, m.Compose2(xflt, m.Compose2( xflr, xfsc ) ) )
  
  ' Create a rectangle with animated color.
  Set rect = m.Rect(.015,.015)
  Set recImg = m.SolidColorImage(clr).Clip(m.FillMatte(rect))
  
  ' Create each of the five squares (numGeos), wait xfID.Duration(i*.3)
  ' and then apply final transformation (xf) to each of them.
  Set finalImg = m.EmptyImage
  For i = 0 To numGeos 
    Set delay = xfID.Duration(i*.3)
    Set newImg = recImg.Transform(m.Sequence(delay,xf))
    Set finalImg = m.Overlay(newImg,finalImg)
  Next 

  ' Set the image into the control
  DAControl.Image = finalImg
  
  ' Set the background in case of a non-windowless browser (like IE3).
  DAControl.BackgroundImage = m.SolidColorImage(m.Blue)     
     
  ' Start the animation.       
  DAControl.Start()
  
End Sub
-->
</SCRIPT>
</BODY>
</HTML>

