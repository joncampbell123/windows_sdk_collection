


subttl	Cursor Exclusion
page

;	Cursor Exclusion
;
;	If either device or both devices are for the display, then
;	the cursor must be excluded.  If both devices are the display,
;	then a union of both rectangles must be performed to determine
;	the exclusion area.
;
;	Currently:
;		SI = X extent
;		DI = Y extent
;
;	Exit:	To setup_300 if not the device
;		To whatever follows if the device


setup_250:
	mov	al,the_flags
	and	al,F0_SRC_IS_DEV+F0_DEST_IS_DEV	;Are both memory bitmaps?
	jz	setup_300		;  Yes, no exclusion needed

	ifdef	Exclusion

	dec	si			;Make the extents inclusive of the
	dec	di			;  last point

	mov	cx,DestxOrg		;Assume only a destination on the
	mov	dx,DestyOrg		;  display
	test	al,F0_SRC_IS_DEV		;Is the source a memory bitmap?
	jz	setup_280		;  Yes, go set right and bottom
	test	al,F0_DEST_IS_DEV		;  (set 'Z' if dest is memory)
	mov	ax,cx			;  No, prepare for the union
	mov	bx,dx

	mov	cx,SrcxOrg		;Set source org
	mov	dx,SrcyOrg
	jz	setup_280		;Dest is memory. Set right and bottom

;	The union of the two rectangles must be performed.  The top left
;	corner will be the smallest x and smallest y.  The bottom right
;	corner will be the largest x and the largest y added into the
;	extents

	cmp	cx,ax			;Get smallest x
	jle	setup_260		;CX is smallest
	xchg	ax,cx			;AX is smallest

setup_260:
	cmp	dx,bx			;Get smallest y
	jle	setup_270		;DX is smallest
	xchg	dx,bx			;BX is smallest

setup_270:
	add	si,ax			;Set right
	add	di,bx			;Set bottom
	jmp	short setup_290		;Go do exclusion

setup_280:
	add	si,cx			;Set right
	add	di,dx			;Set bottom

setup_290:
	call	Exclude 		;Exclude the area from the screen

	endif
