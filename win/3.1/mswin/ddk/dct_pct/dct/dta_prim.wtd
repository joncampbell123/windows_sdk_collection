'-------------------------------------------------------------------------
'
' NAME     : DTA_PRIM.WTD
' DESC     : Uses DTA.EXE to test primitives.
' DATE     : 10-03-91
' 
'-------------------------------------------------------------------------

CONST TEST_NAME = "DTA PRIMITIVE TESTS"      ' command line param
CONST SCRIPT = "DTA_PRIM"

'$include: 'dta.inc'

DECLARE SUB PrimTestMain
DECLARE SUB TestPrim (szPrim AS STRING)
DECLARE SUB SetPoints (nX2%, nY2%, nX3%, nY3%, nX4%, nY4%)
DECLARE SUB SetPolyPoints (nX1%, nY1%, nX2%, nY2%, nX3%, nY3%, nX4%, nY4%)
'--------------------------------------------------------------------------

SUB TestPrim (szPrim AS STRING)
   SelectOptionMenu "P"    ' select primitive dlg
   SetButtonState szPrim, TRUE         ' turn on primitive
   ClickButton "OK"

   '$IFDEF CTSHELL
      drawObject
   '$ELSE
      FOR nLoopCount% = 1 TO MAXREP
         drawObject
      NEXT nLoopCount%
   '$ENDIF

   SelectOptionMenu "P"                ' select primitive dlg
   SetButtonState szPrim, FALSE        ' turn off primitive
   ClickButton "OK"
END SUB

'--------------------------------------------------------------------------

SUB SetPoints (nX2%, nY2%, nX3%, nY3%, nX4%, nY4%)
   SendKeys "%on"          ' select points dlg
   SendKeys "%2%2"         ' goto x2,y2
   SendKeys "{tab}" + TrimStr$ (nX2%)
   SendKeys "{tab}" + TrimStr$ (nY2%)
   SendKeys "{tab 2}" + TrimStr$ (nX3%)
   SendKeys "{tab}" + TrimStr$ (nY3%)
   SendKeys "{tab 2}" + TrimStr$ (nX4%)
   SendKeys "{tab}" + TrimStr$ (nY4%)
   ClickButton "OK"        ' close points dlg
END SUB

'--------------------------------------------------------------------------

SUB SetPolyPoints (nX1%, nY1%, nX2%, nY2%, nX3%, nY3%, nX4%, nY4%)
   SendKeys "%on"                              ' select points dlg
   SendKeys "%l%l"                             ' clear all points
   SendKeys "%x" + TrimStr$ (nX1%)             ' set x coord
   SendKeys "%y" + TrimStr$ (nY1%) + "%a"      ' set y coord, add pt. pair
   SendKeys "%x" + TrimStr$ (nX2%)             ' set x coord
   SendKeys "%y" + TrimStr$ (nY2%) + "%a"      ' set y coord, add pt. pair
   SendKeys "%x" + TrimStr$ (nX3%)             ' set x coord
   SendKeys "%y" + TrimStr$ (nY3%) + "%a"      ' set y coord, add pt. pair
   SendKeys "%x" + TrimStr$ (nX4%)             ' set x coord
   SendKeys "%y" + TrimStr$ (nY4%) + "%a"      ' set y coord, add pt. pair
   ClickButton "OK"
END SUB

'--------------------------------------------------------------------------

SUB PrimTestMain
   DIM lRetCode AS LONG
   lRetCode = SendMessage (hDTA, 10000, 0, 65536 * 9)

   TestName$ = "Primitives"
   StartTest TestName$, 64, 65536 * 9

   TestPrim "Rectangle"
   TestPrim "LineTo"

   SetPoints 150, 150, 30, 30, 30, 30
   TestPrim "RoundRect"

   TestPrim "Ellipse"

   SetPoints 300, 150, 150, 50, 150, 150
   TestPrim "Arc"

   SetPoints 150, 150, 70, 60, 120, 100
   TestPrim "Chord"

   SetPoints 150, 150, 130, 95, 110, 50
   TestPrim "Pie"

   TestPrim "Polygon"
   
   SetPolyPoints 50, 50, 130, 80, 65, 95, 125, 120
   TestPrim "Polyline"

   EndTest TestName$                     ' log results
   
      ' restore primitive menu to original state
   SelectOptionMenu "P"                  ' select primitive dlg
   SetButtonState "Rectangle", TRUE      ' turn on rectangle
   ClickButton "OK"
END SUB

'--------------------------------------------------------------------------
' M A I N
'--------------------------------------------------------------------------

StartScript
InitApp

PrimTestMain
 
EndScript

