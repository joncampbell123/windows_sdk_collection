

<HTML>
<HEAD>
<TITLE>ACC: How to Use the Windows OpenFile Dialog Box (1.x/2.0) </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q96114 ">
<META NAME="KBModify" CONTENT="1997/06/08">
<META NAME="KBCreate" CONTENT="1993/03/08">
<META NAME="Keywords" CONTENT="kbprg PgmApi">
<META NAME="KBArea" CONTENT="Support; KB; access, convert, crossnet, dbweb, odbc, winprint, setup, vbapp, office, officevba">
<META NAME="Description" CONTENT="  Advanced: Requires expert coding, interoperability, and multiuser skills.  This article describes how to use the OpenFile dialog box from Access Basic.  This article assumes that you are familiar with Access Basic and with creating Microsoft Access...">
<META NAME="Product" CONTENT="Access">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT="ODBC ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QANG,QA7H,QA1S,QDN9,QAY5,QDL9,QBWO,QBWN,QA01,QAH6,QAI4,QA2S,QBV8,QAMB,QAH7 V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>ACC: How to Use the Windows OpenFile Dialog Box (1.x/2.0)</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  June 8, 1997</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q96114 </B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
 
The information in this article applies to:

<UL><LI>Microsoft Access versions 1.0, 1.1, 2.0
</UL> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
Advanced: Requires expert coding, interoperability, and multiuser skills.
<P>
This article describes how to use the OpenFile dialog box from Access
Basic.
<P>
This article assumes that you are familiar with Access Basic and with
creating Microsoft Access applications using the programming tools provided
with Microsoft Access. For more information on Access Basic, please refer
to the "Introduction to Programming" manual in Microsoft Access version
1.x, or the "Building Applications" manual in version 2.0.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
The OpenFile common dialog box can be implemented in Microsoft Access
through the use of the Windows GetOpenFileName() application programming
interface (API) function. Using this function will simplify programming
issues for the developer. This function creates a system-defined dialog
box, familiar throughout Windows, that makes it possible for the user to
select a file to open. This function will return a valid file to the
programmer that is fully qualified with the path name. The developer can
customize how the system will handle specific situations, such as
specifying that the file must exist when the user wants to save a file
through the use of flags.
<P>
To use the code below, paste the declarations and function into a module.
<P>
NOTE: You may have some Microsoft Windows API functions defined in an
existing Microsoft Access library; therefore, your declarations may be
duplicates. If you receive a duplicate procedure name error message, remove
or comment out the declarations statement in your code.
<P>
NOTE: In the following sample code, an underscore (_) is used as a line-
continuation character. Remove the underscore from the end of the line
when re-creating this code.
<P>
<PRE>   '-------------------------------------------------------
   ' Global Declaration Section
   '-------------------------------------------------------
   Option Compare Database
   Option Explicit

   Type tagOPENFILENAME
        lStructSize As Long
           hwndOwner As Integer
        hInstance As Integer
        lpstrFilter As Long
        lpstrCustomFilter As Long
        nMaxCustFilter As Long
        nFilterIndex As Long
        lpstrFile As Long
        nMaxFile As Long
        lpstrFileTitle As Long
        nMaxFileTitle As Long
        lpstrInitialDir As Long
        lpstrTitle As Long
        Flags As Long
        nFileOffset As Integer
        nFileExtension As Integer
        lpstrDefExt As Long
        lCustData As Long
           lpfnHook As Long
        lpTemplateName As Long
   End Type

   Declare Function GetOpenFileName% Lib "COMMDLG.DLL" (_
                       OPENFILENAME As tagOPENFILENAME)
   Declare Function GetSaveFileName% Lib "COMMDLG.DLL" (_
                       OPENFILENAME As tagOPENFILENAME)
   Declare Function lstrcpy&amp; Lib "Kernel" (ByVal lpDestString As Any,_
                       ByVal lpSourceString As Any)

   Dim OPENFILENAME As tagOPENFILENAME

   Global Const OFN_READONLY = &amp;H1
   Global Const OFN_OVERWRITEPROMPT = &amp;H2
   Global Const OFN_HIDEREADONLY = &amp;H4
   Global Const OFN_NOCHANGEDIR = &amp;H8
   Global Const OFN_SHOWHELP = &amp;H10
   Global Const OFN_ENABLEHOOK = &amp;H20
   Global Const OFN_ENABLETEMPLATE = &amp;H40
   Global Const OFN_ENABLETEMPLATEHANDLE = &amp;H80
   Global Const OFN_NOVALIDATE = &amp;H100
   Global Const OFN_ALLOWMULTISELECT = &amp;H200
   Global Const OFN_EXTENSIONDIFFERENT = &amp;H400
   Global Const OFN_PATHMUSTEXIST = &amp;H800
   Global Const OFN_FILEMUSTEXIST = &amp;H1000
   Global Const OFN_CREATEPROMPT = &amp;H2000
   Global Const OFN_SHAREAWARE = &amp;H4000
   Global Const OFN_NOREADONLYRETURN = &amp;H8000
   Global Const OFN_NOTESTFILECREATE = &amp;H10000

   Global Const OFN_SHAREFALLTHROUGH = 2
   Global Const OFN_SHARENOWARN = 1
   Global Const OFN_SHAREWARN = 0

   '-------------------------------------------------------
   ' Open Common Dialog Function
   '-------------------------------------------------------
   Function OpenCommDlg ()
     Dim Message$, Filter$, FileName$, FileTitle$, DefExt$
     Dim Title$, szCurDir$, APIResults%

       '*Define the filter string and allocate space in the "c" string
       Filter$ = "Access(*.mdb)" &amp; Chr$(0) &amp; "*.MDB;*.MDA" &amp; Chr$(0)
       Filter$ = Filter$ &amp; "Text(*.txt)" &amp; Chr$(0) &amp; "*.TXT" &amp; Chr$(0)
       Filter$ = Filter$ &amp; "Batch(*.bat)" &amp; Chr$(0) &amp; "*.BAT" &amp; Chr$(0)
       Filter$ = Filter$ &amp; Chr$(0)

       '* Allocate string space for the returned strings.
       FileName$ = Chr$(0) &amp; Space$(255) &amp; Chr$(0)
       FileTitle$ = Space$(255) &amp; Chr$(0)

       '* Give the dialog a caption title.
       Title$ = "My File Open Dialog" &amp; Chr$(0)

       '* If the user does not specify an extension, append TXT.
       DefExt$ = "TXT" &amp; Chr$(0)

       '* Set up the default directory
       szCurDir$ = CurDir$ &amp; Chr$(0)

       '* Set up the data structure before you call the GetOpenFileName

       OPENFILENAME.lStructSize = Len(OPENFILENAME)

       'If the OpenFile Dialog box is linked to a form use this line.
          'It will pass the forms window handle.

       OPENFILENAME.hwndOwner = Screen.ActiveForm.hWnd

       'If the OpenFile Dialog box is not linked to any form use this line.
       'It will pass a null pointer.

       OPENFILENAME.hwndOwner = 0&amp;

       OPENFILENAME.lpstrFilter = lstrcpy(Filter$, Filter$)
       OPENFILENAME.nFilterIndex = 1
       OPENFILENAME.lpstrFile = lstrcpy(FileName$, FileName$)
       OPENFILENAME.nMaxFile = Len(FileName$)
       OPENFILENAME.lpstrFileTitle = lstrcpy(FileTitle$, FileTitle$)
       OPENFILENAME.nMaxFileTitle = Len(FileTitle$)
       OPENFILENAME.lpstrTitle = lstrcpy(Title$, Title$)
       OPENFILENAME.Flags = OFN_FILEMUSTEXIST Or OFN_READONLY
       OPENFILENAME.lpstrDefExt = lstrcpy(DefExt$, DefExt$)
       OPENFILENAME.hInstance = 0
       OPENFILENAME.lpstrCustomFilter = 0
       OPENFILENAME.nMaxCustFilter = 0
       OPENFILENAME.lpstrInitialDir = lstrcpy(szCurDir$, szCurDir$)
       OPENFILENAME.nFileOffset = 0
       OPENFILENAME.nFileExtension = 0
       OPENFILENAME.lCustData = 0
       OPENFILENAME.lpfnHook = 0
       OPENFILENAME.lpTemplateName = 0

       '* This will pass the desired data structure to the Windows API,
       '* which in turn uses it to display the Open Dialog form.

       APIResults% = GetOpenFileName(OPENFILENAME)

       If APIResults% &lt;&gt; 0 Then

           '* Note that FileName$ will have an embedded Chr$(0) at the
           '* end. You may wish to strip this character from the string.

         FileName$ = Left$(FileName$, InStr(FileName$, Chr$(0)) -1)

           Message$ = "The file you chose was " + FileName$
       Else
           Message$ = "No file was selected"
       End If

       MsgBox Message$

   End Function

</PRE><h2>REFERENCES</h2>
 
<P>
"Microsoft Windows Software Development Kit Programmer's Reference,
Volume 2: Functions," version 3.1
<P>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional query words: api common dialog<BR>
Keywords            : kbprg PgmApi<BR>
Version             : 1.0 1.1 2.0<BR>
Platform            : WINDOWS<BR>
Hardware            : X86<BR>
Issue type          : kbhowto<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  June 8, 1997</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
