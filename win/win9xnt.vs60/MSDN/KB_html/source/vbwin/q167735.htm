

<HTML>
<HEAD>
<TITLE>FIX: Setting Printer to Item in the Printers Collection Fails </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q167735">
<META NAME="KBModify" CONTENT="1997/12/18">
<META NAME="KBCreate" CONTENT="1997/04/29">
<META NAME="Keywords" CONTENT="vb5all VBKBPrinting VS97FixlistSP3 kbprint VS97FixlistSP2 VB5FixlistSP2">
<META NAME="KBArea" CONTENT="Support; KB; vbwin">
<META NAME="Description" CONTENT="  Attempting to set the default printer to an object variable has no effect. For instance, given a system with more than one printer installed, the following code will not change the default printer:     Private Sub Form_Load()        Dim Prt As Prin...">
<META NAME="Product" CONTENT="Visual Basic for Windows">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QATX,QACJ,QANF,QA4Q,QAOX,QAFF,QACT,QAH4,QABA,QAY5,QBS0,QAGI,QBWQ,QDL9,QBWO V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>FIX: Setting Printer to Item in the Printers Collection Fails</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  December 18, 1997</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q167735</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
 
The information in this article applies to:

<UL><LI>Microsoft Visual Basic Control Creation, Learning, Professional, and
   Enterprise Editions for Windows, version 5.0
   on the following platforms: NT, Win95
</UL> 
<P>
<P><h2>SYMPTOMS</h2>
 
<P>
Attempting to set the default printer to an object variable has no
effect. For instance, given a system with more than one printer installed,
the following code will not change the default printer:
<P>
<PRE>   Private Sub Form_Load()
       Dim Prt As Printer
       For Each Prt In Printers
      If Not Prt Is Printer Then
            Set Printer = Prt
         Exit For
      End If
       Next

      Printer.Print "Hi, Mom"
      Printer.EndDoc
   End Sub

</PRE>The expected behavior is that the document should print to the first non-
default printer found in the printers collection.
<P>
The actual behavior is that the document prints to the original default
printer.
<P>
<P><h2>RESOLUTION</h2>
 
<P>
*CAUTION* This workaround changes the default printer for all applications
that are currently running.
<P>
The following code provides a means to determine which printers are
available, and to designate one as the default printer.

<OL><P><LI>Add the following code to a module:
<P>
<P><PRE>      Declare Function GetProfileString Lib "kernel32" _
      Alias "GetProfileStringA" _
      (ByVal lpAppName As String, _
      ByVal lpKeyName As String, _
      ByVal lpDefault As String, _
      ByVal lpReturnedString As String, _
      ByVal nSize As Long) As Long
</PRE><P>
<P><PRE>      Declare Function WriteProfileString Lib "kernel32" _
      Alias "WriteProfileStringA" _
      (ByVal lpszSection As String, _
      ByVal lpszKeyName As String, _
      ByVal lpszString As String) As Long
</PRE><P>
<P><PRE>      Declare Function SendMessage Lib "user32" _
      Alias "SendMessageA" _
      (ByVal hwnd As Long, _
      ByVal wMsg As Long, _
      ByVal wParam As Long, _
      lparam As String) As Long
</PRE><P>
<P><PRE>      Public Const HWND_BROADCAST = &amp;HFFFF
      Public Const WM_WININICHANGE = &amp;H1A
</PRE><P>
<P><PRE>      Public Type OSVERSIONINFO
<PRE></PRE>         dwOSVersionInfoSize As Long
         dwMajorVersion As Long
         dwMinorVersion As Long
         dwBuildNumber As Long
         dwPlatformId As Long
         szCSDVersion As String * 128
      End Type

      Declare Function GetVersionExA Lib "kernel32" _
      (lpVersionInformation As OSVERSIONINFO) As Integer

      Public Declare Function OpenPrinter Lib "winspool.drv" _
      Alias "OpenPrinterA" _
      (ByVal pPrinterName As String, _
      phPrinter As Long, _
      pDefault As PRINTER_DEFAULTS) As Long

      Public Declare Function SetPrinter Lib "winspool.drv" _
      Alias "SetPrinterA" _
      (ByVal hPrinter As Long, _
      ByVal Level As Long, _
      pPrinter As Any, _
      ByVal Command As Long) As Long

      Public Declare Function GetPrinter Lib "winspool.drv" _
      Alias "GetPrinterA" _
      (ByVal hPrinter As Long, _
      ByVal Level As Long, _
      pPrinter As Any, _
      ByVal cbBuf As Long, _
      pcbNeeded As Long) As Long

      Public Declare Function lstrcpy Lib "kernel32" _
      Alias "lstrcpyA" _
      (ByVal lpString1 As String, _
      ByVal lpString2 As Any) As Long

      Public Declare Function ClosePrinter Lib "winspool.drv" _
      (ByVal hPrinter As Long) As Long

      Public Declare Function GetLastError Lib "kernel32" () As Long

      ' constants for DEVMODE structure
      Public Const CCHDEVICENAME = 32
      Public Const CCHFORMNAME = 32

      ' constants for DesiredAccess member of PRINTER_DEFAULTS
      Public Const STANDARD_RIGHTS_REQUIRED = &amp;HF0000
      Public Const PRINTER_ACCESS_ADMINISTER = &amp;H4
      Public Const PRINTER_ACCESS_USE = &amp;H8
      Public Const PRINTER_ALL_ACCESS = (STANDARD_RIGHTS_REQUIRED Or _
      PRINTER_ACCESS_ADMINISTER Or PRINTER_ACCESS_USE)

      ' constant that goes into PRINTER_INFO_5 Attributes member
      ' to set it as default
      Public Const PRINTER_ATTRIBUTE_DEFAULT = 4

      Public Type DEVMODE
              dmDeviceName As String * CCHDEVICENAME
              dmSpecVersion As Integer
              dmDriverVersion As Integer
              dmSize As Integer
              dmDriverExtra As Integer
              dmFields As Long
              dmOrientation As Integer
              dmPaperSize As Integer
              dmPaperLength As Integer
              dmPaperWidth As Integer
              dmScale As Integer
              dmCopies As Integer
              dmDefaultSource As Integer
              dmPrintQuality As Integer
              dmColor As Integer
              dmDuplex As Integer
              dmYResolution As Integer
              dmTTOption As Integer
              dmCollate As Integer
              dmFormName As String * CCHFORMNAME
              dmLogPixels As Integer
              dmBitsPerPel As Long
              dmPelsWidth As Long
              dmPelsHeight As Long
              dmDisplayFlags As Long
              dmDisplayFrequency As Long
              dmICMMethod As Long        ' // Windows 95 only
              dmICMIntent As Long        ' // Windows 95 only
              dmMediaType As Long        ' // Windows 95 only
              dmDitherType As Long       ' // Windows 95 only
              dmReserved1 As Long        ' // Windows 95 only
              dmReserved2 As Long        ' // Windows 95 only
      End Type

      Public Type PRINTER_INFO_5
              pPrinterName As String
              pPortName As String
              Attributes As Long
              DeviceNotSelectedTimeout As Long
              TransmissionRetryTimeout As Long
      End Type

      Public Type PRINTER_DEFAULTS
              pDatatype As Long
              pDevMode As DEVMODE
              DesiredAccess As Long
      End Type

</PRE><P><LI>Place a Listbox and a CommandButton on a form. Add the following code
   to the General Declarations section of the form:
<P>
<P><PRE>      Option Explicit
</PRE><P>
<P><PRE>      Private Function PtrCtoVbString(Add As Long) As String
</PRE><P>
<PRE>          Dim sTemp As String * 512, x As Long

          x = lstrcpy(sTemp, Add)
          If (InStr(1, sTemp, Chr(0)) = 0) Then
               PtrCtoVbString = ""
          Else
               PtrCtoVbString = Left(sTemp, InStr(1, sTemp, Chr(0)) - 1)
          End If
      End Function
      Private Sub SetDefaultPrinter(ByVal PrinterName As String, _
          ByVal DriverName As String, ByVal PrinterPort As String)
          Dim DeviceLine As String
          Dim r As Long
          Dim l As Long
          DeviceLine = PrinterName &amp; "," &amp; DriverName &amp; "," &amp; PrinterPort
          ' Store the new printer information in the [WINDOWS] section of
          ' the WIN.INI file for the DEVICE= item
          r = WriteProfileString("windows", "Device", DeviceLine)
          ' Cause all applications to reload the INI file:
          l = SendMessage(HWND_BROADCAST, WM_WININICHANGE, 0, "windows")
      End Sub

      Private Sub Win95SetDefaultPrinter()
          Dim Handle As Long          'handle to printer
          Dim PrinterName As String
          Dim pd As PRINTER_DEFAULTS
          Dim x As Long
          Dim need As Long            ' bytes needed
          Dim pi5 As PRINTER_INFO_5   ' your PRINTER_INFO structure
          Dim LastError As Long

          ' determine which printer was selected
          PrinterName = List1.List(List1.ListIndex)
          ' none - exit
          If PrinterName = "" Then
              Exit Sub
          End If

          ' set the PRINTER_DEFAULTS members
          pd.pDatatype = 0&amp;
          pd.DesiredAccess = PRINTER_ALL_ACCESS

          ' Get a handle to the printer
          x = OpenPrinter(PrinterName, Handle, pd)
          ' failed the open
          If x = False Then
              'error handler code goes here
              Exit Sub
          End If

          ' Make an initial call to GetPrinter, requesting Level 5
          ' (PRINTER_INFO_5) information, to determine how many bytes
          ' you need
          x = GetPrinter(Handle, 5, ByVal 0&amp;, 0, need)
          ' don't want to check GetLastError here - it's supposed to fail
          ' with a 122 - ERROR_INSUFFICIENT_BUFFER
          ' redim t as large as you need
          ReDim t((need \ 4)) As Long

          ' and call GetPrinter for keepers this time
          x = GetPrinter(Handle, 5, t(0), need, need)
          ' failed the GetPrinter
          If x = False Then
              'error handler code goes here
              Exit Sub
          End If

          ' set the members of the pi5 structure for use with SetPrinter.
          ' PtrCtoVbString copies the memory pointed at by the two string
          ' pointers contained in the t() array into a Visual Basic string.
          ' The other three elements are just DWORDS (long integers) and
          ' don't require any conversion
          pi5.pPrinterName = PtrCtoVbString(t(0))
          pi5.pPortName = PtrCtoVbString(t(1))
          pi5.Attributes = t(2)
          pi5.DeviceNotSelectedTimeout = t(3)
          pi5.TransmissionRetryTimeout = t(4)

          ' this is the critical flag that makes it the default printer
          pi5.Attributes = PRINTER_ATTRIBUTE_DEFAULT

          ' call SetPrinter to set it
          x = SetPrinter(Handle, 5, pi5, 0)
          ' failed the SetPrinter
          If x = False Then
              MsgBox "SetPrinterFailed. Error code: " &amp; GetLastError()
              Exit Sub
          End If

          ' and close the handle
          ClosePrinter (Handle)

      End Sub
      Private Sub GetDriverAndPort(ByVal Buffer As String, DriverName As
      String, _PrinterPort As String)
          Dim iDriver As Integer
          Dim iPort As Integer
          DriverName = ""
          PrinterPort = ""

          'The driver name is first in the string terminated by a comma
          iDriver = InStr(Buffer, ",")
          If iDriver &gt; 0 Then

              'Strip out the driver name
              DriverName = Left(Buffer, iDriver - 1)

              'The port name is the second entry after the driver name
              'separated by commas.
              iPort = InStr(iDriver + 1, Buffer, ",")

              If iPort &gt; 0 Then
                  'Strip out the port name
                  PrinterPort = Mid(Buffer, iDriver + 1, _
                  iPort - iDriver - 1)
              End If
          End If
      End Sub

        Private Sub ParseList(lstCtl As Control, ByVal Buffer As String)
          Dim i As Integer

          Dim s As String

          Do
              i = InStr(Buffer, Chr(0))
              If i &gt; 0 Then
                  s = Left(Buffer, i - 1)
                  If Len(Trim(s)) Then lstCtl.AddItem s
                  Buffer = Mid(Buffer, i + 1)
              Else
                  If Len(Trim(Buffer)) Then lstCtl.AddItem Buffer
                  Buffer = ""
              End If
          Loop While i &gt; 0
      End Sub
      Private Sub WinNTSetDefaultPrinter()
          Dim Buffer As String
          Dim DeviceName As String
          Dim DriverName As String
          Dim PrinterPort As String
          Dim PrinterName As String
          Dim r As Long
          If List1.ListIndex &gt; -1 Then
              'Get the printer information for the currently selected
              'printer in the list. The information is taken from the
              'WIN.INI file.
              Buffer = Space(1024)
              PrinterName = List1.Text
              r = GetProfileString("PrinterPorts", PrinterName, "", _
                  Buffer, Len(Buffer))

              'Parse the driver name and port name out of the buffer
              GetDriverAndPort Buffer, DriverName, PrinterPort

              If DriverName &lt;&gt; "" And PrinterPort &lt;&gt; "" Then
                  SetDefaultPrinter List1.Text, DriverName, PrinterPort
              End If
          End If
      End Sub

      Private Sub Command1_Click()
          Dim osinfo As OSVERSIONINFO
          Dim retvalue As Integer

          osinfo.dwOSVersionInfoSize = 148
          osinfo.szCSDVersion = Space$(128)
          retvalue = GetVersionExA(osinfo)

      If osinfo.dwMajorVersion = 3 And osinfo.dwMinorVersion = 51 And _
          osinfo.dwBuildNumber = 1057 And osinfo.dwPlatformId = 2 Then
              Call WinNTSetDefaultPrinter
          ElseIf osinfo.dwMajorVersion = 4 And osinfo.dwMinorVersion = 0 _
          And osinfo.dwBuildNumber = 67109814 And osinfo.dwPlatformId = 1 _
          Then
              Call Win95SetDefaultPrinter
          ElseIf osinfo.dwMajorVersion = 4 And osinfo.dwMinorVersion = 0 _
          And osinfo.dwBuildNumber = 1381 And osinfo.dwPlatformId = 2 Then
              Call WinNTSetDefaultPrinter
          End If
      End Sub

      Private Sub Form_Load()
          Dim r As Long
          Dim Buffer As String

          'Get the list of available printers from WIN.INI
          Buffer = Space(8192)
          r = GetProfileString("PrinterPorts", vbNullString, "", _
             Buffer, Len(Buffer))

          'Display the list of printer in the list box List1
          ParseList List1, Buffer
      End Sub

</PRE></OL>The code above will populate a list box with all available printers. The
user can then select one printer and make it the default by clicking the
CommandButton.
<P>
<P><h2>STATUS</h2>
 
<P>
Microsoft has confirmed this to be a bug in the Microsoft products listed
at the beginning of this article. This bug has been fixed in Visual Studio
97 Service Pack 2.
<P>
For more information on the Visual Studio 97 Service Pack 2, please see the
following article in the Microsoft Knowledge Base:
<P>
<PRE>   ARTICLE-ID: <B><A href="../vstudio/q170365.htm">Q170365</A></B>
   TITLE     : INFO: Visual Studio 97 Service Packs - What, Where, and Why

</PRE>For a list of the Visual Basic 5.0 bugs that were fixed in the Visual
Studio 97 Service Pack 2, please see the following article in the Microsoft
Knowledge Base:
<P>
<PRE>   ARTICLE-ID: <B><A href="../vbwin/q171554.htm">Q171554</A></B>
   TITLE     : INFO: Visual Basic 5.0 Fixes in Visual Studio 97
               Service Pack 2

</PRE><h2>REFERENCES</h2>
 
<P>
For more information, please see the following article in the Microsoft
Knowledge Base:
<P>
<PRE>   ARTICLE-ID: <B><A href="../vbwin/q142388.htm">Q142388</A></B>
   TITLE     : Changing WIN.INI Printer Settings from VB using Windows API
</PRE>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional query words: setting default printer<BR>
Keywords          : vb5all VBKBPrinting VS97FixlistSP3 kbprint VS97FixlistSP2 VB5FixlistSP2<BR>
Version           : 5.0<BR>
Platform          : NT Win95 WINDOWS<BR>
Issue type        : kbbug<BR>
Solution Type     : kbfix kbservicepack<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  December 18, 1997</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
