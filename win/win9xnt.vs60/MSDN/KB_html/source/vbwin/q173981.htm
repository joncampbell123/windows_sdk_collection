

<HTML>
<HEAD>
<TITLE>PRB: Working with Print Dialog and Printer Object under NT 4.0 </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q173981">
<META NAME="KBModify" CONTENT="1998/03/19">
<META NAME="KBCreate" CONTENT="1997/09/17">
<META NAME="Keywords" CONTENT="VB4ALL VB4WIN vb5all VBKBPrinting kbprint">
<META NAME="KBArea" CONTENT="Support; KB; vbwin">
<META NAME="Description" CONTENT="  With Microsoft Visual Basic versions 4.0 and 5.0 on Windows NT version 4.0, when you use the ShowPrinter method of the Common Dialog Control to allow the user to set properties of the Printer object, such as the Copies and Orientation properties, t...">
<META NAME="Product" CONTENT="Visual Basic for Windows">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Security" CONTENT="PUBLIC ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QA4Q,QAOX,QA0K,QAPN,QACT,QA01,QBS0,QA9N,QASB,QBXS,QBWP,QABA,QAAL,QATX,QAH4 V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>PRB: Working with Print Dialog and Printer Object under NT 4.0</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  March 19, 1998</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q173981</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
 
The information in this article applies to:

<UL><LI>Microsoft Visual Basic Learning, Professional, and Enterprise Editions
   for Windows, version 5.0
   on the following platform: NT
<LI>Microsoft Visual Basic Standard, Professional, and Enterprise Editions,
   16-bit and 32-bit, for Windows, version 4.0
   on the following platform: NT
</UL> 
<P>
<P><h2>SYMPTOMS</h2>
 
<P>
With Microsoft Visual Basic versions 4.0 and 5.0 on Windows NT version 4.0,
when you use the ShowPrinter method of the Common Dialog Control to allow
the user to set properties of the Printer object, such as the Copies and
Orientation properties, these properties are not affected by the selection
in the Print dialog box.
<P>
<P><h2>RESOLUTION</h2>
 
<P>
To work around this problem, use the Windows API to display the Printer
dialog and then set the Printer object's properties to those selected by
the user. The following steps illustrate this solution:

<OL><P><LI>Start a new project with the default Form1.

<P><LI>Add two Command buttons to Form1. Set the following properties for the
   controls:
<P>
<PRE>                                Name Property         Caption Property
                                -------------         ----------------
   First Command button :       cmdPrint              Print Dialog
   Second Command button:       cmdPrintSetup         Print Setup Dialog

</PRE><P><LI>Add a module to the project.

<P><LI>Add the following code to the module:
<P>
<P><PRE>      ' Global constants for Win32 API
      Public Const CCHDEVICENAME = 32
      Public Const CCHFORMNAME = 32
      Public Const GMEM_FIXED = &amp;H0
      Public Const GMEM_MOVEABLE = &amp;H2
      Public Const GMEM_ZEROINIT = &amp;H40
      Public Const DM_DUPLEX = &amp;H1000&amp;
      Public Const DM_ORIENTATION = &amp;H1&amp;
      Public Const PD_ALLPAGES = &amp;H0
      Public Const PD_COLLATE = &amp;H10
      Public Const PD_DISABLEPRINTTOFILE = &amp;H80000
      Public Const PD_ENABLEPRINTHOOK = &amp;H1000
      Public Const PD_ENABLEPRINTTEMPLATE = &amp;H4000
      Public Const PD_ENABLEPRINTTEMPLATEHANDLE = &amp;H10000
      Public Const PD_ENABLESETUPHOOK = &amp;H2000
      Public Const PD_ENABLESETUPTEMPLATE = &amp;H8000
      Public Const PD_ENABLESETUPTEMPLATEHANDLE = &amp;H20000
      Public Const PD_HIDEPRINTTOFILE = &amp;H100000
      Public Const PD_NONETWORKBUTTON = &amp;H200000
      Public Const PD_NOPAGENUMS = &amp;H8
      Public Const PD_NOSELECTION = &amp;H4
      Public Const PD_NOWARNING = &amp;H80
      Public Const PD_PAGENUMS = &amp;H2
      Public Const PD_PRINTSETUP = &amp;H40
      Public Const PD_PRINTTOFILE = &amp;H20
      Public Const PD_RETURNDC = &amp;H100
      Public Const PD_RETURNDEFAULT = &amp;H400
      Public Const PD_RETURNIC = &amp;H200
      Public Const PD_SELECTION = &amp;H1
      Public Const PD_SHOWHELP = &amp;H800
      Public Const PD_USEDEVMODECOPIES = &amp;H40000
      Public Const PD_USEDEVMODECOPIESANDCOLLATE = &amp;H40000
</PRE><P>
<P><PRE>      'Custom Global Constants
      Public Const DLG_PRINT = 0
      Public Const DLG_PRINTSETUP = 1
</PRE><P>
<P><PRE>      'type definitions:
      Type PRINTDLG_TYPE
<PRE></PRE>              lStructSize As Long
              hwndOwner As Long
              hDevMode As Long
              hDevNames As Long
              hdc As Long
              flags As Long
              nFromPage As Integer
              nToPage As Integer
              nMinPage As Integer
              nMaxPage As Integer
              nCopies As Integer
              hInstance As Long
              lCustData As Long
              lpfnPrintHook As Long

              lpfnSetupHook As Long
              lpPrintTemplateName As String
              lpSetupTemplateName As String
              hPrintTemplate As Long
              hSetupTemplate As Long
      End Type

      Type DEVNAMES_TYPE
              wDriverOffset As Integer
              wDeviceOffset As Integer
              wOutputOffset As Integer
              wDefault As Integer
              extra As String * 100
      End Type

      Type DEVMODE_TYPE
              dmDeviceName As String * CCHDEVICENAME
              dmSpecVersion As Integer
              dmDriverVersion As Integer
              dmSize As Integer
              dmDriverExtra As Integer
              dmFields As Long
              dmOrientation As Integer
              dmPaperSize As Integer
              dmPaperLength As Integer
              dmPaperWidth As Integer
              dmScale As Integer
              dmCopies As Integer
              dmDefaultSource As Integer
              dmPrintQuality As Integer
              dmColor As Integer
              dmDuplex As Integer
              dmYResolution As Integer
              dmTTOption As Integer
              dmCollate As Integer
              dmFormName As String * CCHFORMNAME
              dmUnusedPadding As Integer
              dmBitsPerPel As Integer
              dmPelsWidth As Long
              dmPelsHeight As Long
              dmDisplayFlags As Long
              dmDisplayFrequency As Long
      End Type

      'API declarations:
      Public Declare Function PrintDialog Lib "comdlg32.dll" _
         Alias "PrintDlgA" (pPrintdlg As PRINTDLG_TYPE) As Long

      Public Declare Sub CopyMemory Lib "kernel32" _
         Alias "RtlMoveMemory" _
         (hpvDest As Any, hpvSource As Any, ByVal cbCopy As Long)

      Public Declare Function GlobalLock Lib "kernel32" _
         (ByVal hMem As Long) As Long

      Public Declare Function GlobalUnlock Lib "kernel32" _
         (ByVal hMem As Long) As Long

      Public Declare Function GlobalAlloc Lib "kernel32" _
         (ByVal wFlags As Long, ByVal dwBytes As Long) As Long

      Public Declare Function GlobalFree Lib "kernel32" _
         (ByVal hMem As Long) As Long

      Public Sub ShowPrinter(frmOwner as Form, _
          Optional PrintFlags As Integer)

          Dim PrintDlg As PRINTDLG_TYPE
          Dim DevMode As DEVMODE_TYPE
          Dim DevName As DEVNAMES_TYPE

          Dim lpDevMode As Long, lpDevName As Long
          Dim bReturn As Integer
          Dim objPrinter As Printer, NewPrinterName As String
          Dim strSetting as String

          ' Use PrintDialog to get the handle to a memory
          ' block with a DevMode and DevName structures

          PrintDlg.lStructSize = Len(PrintDlg)
          PrintDlg.hwndOwner = frmOwner.hWnd

          PrintDlg.flags = PrintFlags

          'Set the current orientation and duplex setting
          DevMode.dmDeviceName = Printer.DeviceName
          DevMode.dmSize = Len(DevMode)
          DevMode.dmFields = DM_ORIENTATION Or DM_DUPLEX
          DevMode.dmOrientation = Printer.Orientation
          On Error Resume Next
          DevMode.dmDuplex = Printer.Duplex
          On Error GoTo 0

          'Allocate memory for the initialization hDevMode structure
          'and copy the settings gathered above into this memory
          PrintDlg.hDevMode = GlobalAlloc(GMEM_MOVEABLE Or _
             GMEM_ZEROINIT, Len(DevMode))
          lpDevMode = GlobalLock(PrintDlg.hDevMode)
          If lpDevMode &gt; 0 Then
              CopyMemory ByVal lpDevMode, DevMode, Len(DevMode)
              bReturn = GlobalUnlock(PrintDlg.hDevMode)
          End If

          'Set the current driver, device, and port name strings
          With DevName
              .wDriverOffset = 8
              .wDeviceOffset = .wDriverOffset + 1 + Len(Printer.DriverName)
              .wOutputOffset = .wDeviceOffset + 1 + Len(Printer.Port)
              .wDefault = 0
          End With
          With Printer
              DevName.extra = .DriverName &amp; Chr(0) &amp; _
              .DeviceName &amp; Chr(0) &amp; .Port &amp; Chr(0)
          End With

          'Allocate memory for the initial hDevName structure
          'and copy the settings gathered above into this memory
          PrintDlg.hDevNames = GlobalAlloc(GMEM_MOVEABLE Or _
              GMEM_ZEROINIT, Len(DevName))
          lpDevName = GlobalLock(PrintDlg.hDevNames)
          If lpDevName &gt; 0 Then
              CopyMemory ByVal lpDevName, DevName, Len(DevName)
              bReturn = GlobalUnlock(lpDevName)
          End If

          'Call the print dialog up and let the user make changes
          If PrintDialog(PrintDlg) Then

              'First get the DevName structure.
              lpDevName = GlobalLock(PrintDlg.hDevNames)
                  CopyMemory DevName, ByVal lpDevName, 45
              bReturn = GlobalUnlock(lpDevName)
              GlobalFree PrintDlg.hDevNames

              'Next get the DevMode structure and set the printer
              'properties appropriately
              lpDevMode = GlobalLock(PrintDlg.hDevMode)
                  CopyMemory DevMode, ByVal lpDevMode, Len(DevMode)
              bReturn = GlobalUnlock(PrintDlg.hDevMode)
              GlobalFree PrintDlg.hDevMode
              NewPrinterName = UCase$(Left(DevMode.dmDeviceName, _
                  InStr(DevMode.dmDeviceName, Chr$(0)) - 1))
              If Printer.DeviceName &lt;&gt; NewPrinterName Then
                  For Each objPrinter In Printers
                     If UCase$(objPrinter.DeviceName) = NewPrinterName Then
                          Set Printer = objPrinter
                     End If
                  Next
              End If
              On Error Resume Next

              'Set printer object properties according to selections made
              'by user
              With Printer
                  .Copies = DevMode.dmCopies
                  .Duplex = DevMode.dmDuplex
                  .Orientation = DevMode.dmOrientation
              End With
              On Error GoTo 0
          End If

          'Display the results in the immediate (debug) window
          With Printer
              If .Orientation = 1 Then
                  strSetting = "Portrait. "
              Else
                  strSetting = "Landscape. "
              End If
              Debug.Print "Copies = " &amp; .Copies, "Orientation = " &amp; _
                 strSetting
          End With

      End Sub

</PRE><P><LI>Add the following code to Form1:
<P>
<P><PRE>      Private Sub cmdPrint_Click()
<PRE></PRE>         ShowPrinter Me
      End Sub

      Private Sub cmdPrintSetup_Click()
         ShowPrinter Me, PD_PRINTSETUP
      End Sub

</PRE><P><LI>Press the F5 key to run the application.

<P><LI>Click the Command buttons. Note that the correct settings for Copies
   and Orientation are displayed in the Immediate Window (or Debug Window).
<P>
</OL><h2>STATUS</h2>
 
<P>
This behavior is by design.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
<P><h3>Steps to Reproduce Behavior</h3>
 

<OL><P><LI>Start a new project on an NT 4.0 machine with default Form1.

<P><LI>Add a CommandButton and a Common Dialog control to Form1.

<P><LI>Add the following code to the Click event of Command1:
<P>
<P><PRE>      CommonDialog1.ShowPrinter
      Debug.Print "Copies = " &amp; Printer.Copies; " &amp; Orientation = " &amp; _
<PRE></PRE>          Printer.Orientation

</PRE><P><LI>Press the F5 key to run the application.

<P><LI>Click Command1. Change the Number of Copies and Orientation settings in
   the Print Dialog Box and click OK.
<P>
   Result: The line "Copies = 1 &amp; Orientation = 1" is printed in the Debug
<PRE>           (Immediate window) regardless of the settings you selected in
           the Printer Dialog box.

</PRE></OL>NOTE: This same project would produce the expected results on Windows 95.
Copies would equal the Number of Copies you selected and the Orientation
would be 1 if you selected Portrait and 2 if you selected Landscape.
 
<PRE>Keywords          : VB4ALL VB4WIN vb5all VBKBPrinting kbprint
Version           : WINDOWS:4.0,5.0
Platform          : NT WINDOWS
Issue type        : kbprb</PRE>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
================================================================================<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  March 19, 1998</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
