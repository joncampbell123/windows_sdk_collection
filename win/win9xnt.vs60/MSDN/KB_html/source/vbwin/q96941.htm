

<HTML>
<HEAD>
<TITLE>Using the Printer Object to Print a Grid Control's Contents </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q96941 ">
<META NAME="KBModify" CONTENT="1995/06/21">
<META NAME="KBCreate" CONTENT="1993/03/31">
<META NAME="Keywords" CONTENT="kbprint kbprg kbcode">
<META NAME="KBArea" CONTENT="Support; KB; vbwin">
<META NAME="Description" CONTENT="  The example program in this article shows you how to print the contents of a grid control using the Printer object.  MORE INFORMATION  The example code prints a line border around the grid if the grid control BorderStyle is set to 1 and prints grid...">
<META NAME="Product" CONTENT="Visual Basic for Windows">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QARR,QA5V,QAOX,QAUY,QANF,QBS0,QACT,QAIB,QAGB,QBXS,QBJZ,QAH4,QAMN,QAB5,QA7O V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>Using the Printer Object to Print a Grid Control's Contents</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  June 21, 1995</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q96941 </B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
The information in this article applies to:
<P>
- Standard and Professional Editions of Microsoft Visual Basic for
<PRE>  Windows, versions 1.0, 2.0, and 3.0
</PRE> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
The example program in this article shows you how to print the contents of
a grid control using the Printer object.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
The example code prints a line border around the grid if the grid control
BorderStyle is set to 1 and prints grid lines between the cells if
GridLines is set to True.
<P>
<P><h3>Steps to Create Example Program</h3>
 
1. Start Visual Basic or from the File menu, choose New Project (ALT, F, N)
<PRE>   if Visual Basic is already running.

</PRE><P><LI>From the File menu, choose Add File. Select GRID.VBX. The Grid tool
   appears in the Toolbox.

<P><LI>Place a grid (Grid1) on Form1. Set the Cols and Rows properties to 6.

<P><LI>Add the following code to the Form1 Click event:
<P>
<PRE>   Sub Form_Click ()
      ' Add sample data to the grid
      Dim i, j
      For i = 0 To Grid1.Cols - 1
         For j = 0 To Grid1.Rows - 1
            Grid1.Col = i
            Grid1.Row = j
            Grid1.Text = Format$(i + j + i ^ j)
         Next
      Next
      ' Print the data
      Call Grid_Print(Grid1)
      Printer.EndDoc
   End Sub

</PRE><P><LI>Add the following code to the general declarations section:
<P>
<PRE>   Sub Grid_Print (grid As Control)
      Dim tppx As Integer  ' alias TwipsPerPixelX
      Dim tppy As Integer  ' alias TwipsPerPixelY
      tppx = Printer.TwipsPerPixelX
      tppy = Printer.TwipsPerPixelY
      Dim Col As Integer   ' index to grid columns
      Dim Row As Integer   ' index to grid rows
      Dim x0 As Single     ' upper left corner
      Dim y0 As Single     '   "
      Dim x1 As Single     ' position of text
      Dim y1  As Single    '   "
      Dim x2  As Single    ' position of grid lines
      Dim y2  As Single    '   "

      ' set upper left corner
      x0 = Printer.CurrentX
      y0 = Printer.CurrentY

      ' draw the border around the grid
      If grid.BorderStyle &lt;&gt; 0 Then
         Printer.Line -Step(grid.Width - tppx, grid.Height - tppy), , B
         x0 = x0 + tppx
         y0 = y0 + tppy
      End If

      ' draw the text in the grid
      x1 = x0
      For Col = 0 To grid.Cols - 1
         ' skip non-visible columns
         If Col &gt;= grid.FixedCols And Col &lt; grid.LeftCol Then
            Col = grid.LeftCol
         End If
         ' stop if outside grid
         If x1 + grid.ColWidth(Col) &gt;= grid.Width Then Exit For
         y1 = y0
         For Row = 0 To grid.Rows - 1
            ' skip non-visible columns
            If Row &gt;= grid.FixedRows And Row &lt; grid.TopRow Then
               Row = grid.TopRow
            End If
            ' stop if outside grid
            If y1 + grid.RowHeight(Row) &gt;= grid.Height Then Exit For
            ' set position to print the cell
            Printer.CurrentX = x1 + tppx * 2
            Printer.CurrentY = y1 + tppy
            ' print cell text
            grid.Col = Col
            grid.Row = Row
            Printer.Print grid.Text
            ' advance to next row
            y1 = y1 + grid.RowHeight(Row)
            If grid.GridLines Then
               y1 = y1 + tppy
            End If
         Next
         ' advance to next column
         x1 = x1 + grid.ColWidth(Col)
         If grid.GridLines Then
            x1 = x1 + tppx
         End If
      Next

      ' draw grid lines
      If grid.GridLines Then
         x2 = x0
         y2 = y0
         For Col = 0 To grid.Cols - 1
            ' skip non-visible columns
            If Col &gt;= grid.FixedCols And Col &lt; grid.LeftCol Then
               Col = grid.LeftCol
            End If
            x2 = x2 + grid.ColWidth(Col)
            ' stop if outside grid
            If x2 &gt;= grid.Width Then Exit For
            Printer.Line (x2, y0)-Step(0, y1 - tppy)
            x2 = x2 + tppx
         Next
         For Row = 0 To grid.Rows - 1
            ' skip non-visible rows
            If Row &gt;= grid.FixedRows And Row &lt; grid.TopRow Then
               Row = grid.TopRow
            End If
            y2 = y2 + grid.RowHeight(Row)
            ' stop if outside grid
            If y2 &gt;= grid.Height Then Exit For
            Printer.Line (x0, y2)-Step(x1 - tppx, 0)
            y2 = y2 + tppy
         Next
      End If
   End Sub

</PRE><P><LI>Press the F5 key to run the program. Click Form1 to fill the grid with
   sample data and print the grid.
<P></OL>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional reference words: 1.00 2.00 3.00<BR>
KBCategory: kbprint kbprg kbcode<BR>
KBSubcategory: APrgPrint<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  June 21, 1995</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
