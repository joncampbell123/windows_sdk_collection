

<HTML>
<HEAD>
<TITLE>How to Build Access DB &amp; Load Data from Btrieve for Windows DB </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q103440">
<META NAME="KBModify" CONTENT="1995/06/21">
<META NAME="KBCreate" CONTENT="1993/08/24">
<META NAME="Keywords" CONTENT="kbinterop kbprg kbcode">
<META NAME="KBArea" CONTENT="Support; KB; vbwin">
<META NAME="Description" CONTENT="  The example in this article demonstrates how to build a Microsoft Access database without having a database or database template already built. The example uses a Btrieve for Windows database file to supply the data to be placed into the newly crea...">
<META NAME="Product" CONTENT="Visual Basic for Windows">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QALX,QAM9,QAUM,QANF,QDMP,QBXS,QA1S,QAZV,QBS0,QA7O,QA07,QAH4,QDMD,QBWS,QBFS V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>How to Build Access DB &amp; Load Data from Btrieve for Windows DB</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  June 21, 1995</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q103440</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
The information in this article applies to:
<P>
- Microsoft Visual Basic programming system for Windows, version 3.0
 
<P>
<P><h2>SUMMARY</h2>
 
<P>
The example in this article demonstrates how to build a Microsoft Access
database without having a database or database template already built. The
example uses a Btrieve for Windows database file to supply the data to be
placed into the newly created Microsoft Access database.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
NOTE: You will need to have a Btrieve for Windows database file
<PRE>      already built to test this example. The Btrieve for Windows
      database file tested with this example can be sent upon request.

</PRE><h3>Steps to demonstrate the example</h3>
 

<OL><P><LI>Start Visual Basic or from the File menu, choose New Project (ALT, F, N)
   if Visual Basic is already running. Form1 is created by default.

<P><LI>Add three command buttons and two grid controls using GRID.VBX to Form1.
   Using the following table as a guide, set the properties of the controls
   you added in step 2.
<P>
<PRE>   Control    Property   New Value
   ----------------------------------------------------------------------
   Command1   Caption    "Press to Load Btrieve File and Display in Grid"
   Command2   Caption    "Press to Transfer Data and Build New DB"
   Command3   Caption    "Press to Display Data from the New Database"
   Grid1      Cols       4
   Grid1      Rows       15
   Grid2      Cols       4
   Grid2      Rows       15

</PRE><P><LI>Review the following brief outline of the table from the Btrieve for
   Windows database:
<P>
<PRE>   Table Name:     Big_Tab

   Field Names    Field Type      Field Size
   -----------------------------------------
   PrimaryKey     Long Integer
   MyMoney        Currency
   MyString       Text            154

   Index Names    Index Fields      Unique   Primary
   ---------------------------------------------------
   tabindex       +PrimaryKey       Yes      No

</PRE><P><LI>Add the following variables and constants to the (general) section of
   Form1:
<P>
   Dim PrimaryKeys(30) As Long
   Dim Money(30) As Currency
   Dim Strings(30) As String * 154
   Const DB_LONG = 4
   Const DB_TEXT = 10
   Const DB_CURRENCY = 5
   Const DB_LANG_GENERAL = ";LANGID=0x0809;CP=1252;COUNTRY=0"

<P><LI>Add the following code to the Form Load event procedure:
<P>
<PRE>   Sub Form_Load ()
      Show
      grid1.ColWidth(1) = 1000      'For PK ID
      grid1.ColWidth(2) = 2000      'For Money
      grid1.ColWidth(3) = 5000      'For Story
      grid1.Col = 1
      grid1.Row = 0
      grid1.Text = "Primary Keys"   'Header for PK ID
      grid1.Col = 2
      grid1.Row = 0
      grid1.Text = "Money"          'Header for Money
      grid1.Col = 3
      grid1.Row = 0
      grid1.Text = "Big String"     'Header for Story
      grid2.ColWidth(1) = 1000      'For PK ID
      grid2.ColWidth(2) = 2000      'For Money
      grid2.ColWidth(3) = 5000      'For Story
      grid2.Col = 1
      grid2.Row = 0
      grid2.Text = "Prime's"        'Header for PK ID
      grid2.Col = 2
      grid2.Row = 0
      grid2.Text = "Your Money"     'Header for Money
      grid2.Col = 3
      grid2.Row = 0
      grid2.Text = "Your Story"     'Header for Story
   End Sub

</PRE><P><LI>Add the following code to the Command1 Click event procedure:
<P>
<PRE>   Sub Command1_Click ()
      Dim db As Database
      Dim conn$
      Dim dt As Table
      conn$ = "Btrieve;"
      ' Enter the following Set as one, single line:
      Set db = OpenDatabase("C:\articles\btrvwin\file.ddf", False, False,
         conn$)
      Set dt = db.OpenTable("Big_Tab")
      ' Counter for loading the grid
      For i% = 1 To 10       'Grab the first ten for a test
         grid1.Col = 1
         grid1.Row = i%
         grid1.Text = dt(0)       'Load the grid
         PrimaryKeys(i%) = dt(0)  'Load the temporary array
         grid1.Col = 2
         grid1.Row = i%
         grid1.Text = dt(1)       'Load the grid
         Money(i%) = dt(1)        'Load the temporary array
         grid1.Col = 3
         grid1.Row = i%
         grid1.Text = dt(2)       'Load the grid
         Strings(i%) = dt(2)      'Load the temporary array
         dt.MoveNext
      Next i%
   End Sub

</PRE><P><LI>Add the following code to the Command2 Click event procedure:
<P>
<PRE>   Sub Command2_Click ()
      Dim newdb As Database
      Dim newtb As Table
      Dim newtd As New tabledef
      Dim newidx As New Index
      Dim field1 As New field     'For PK IDs
      Dim field2 As New field     'For Money
      Dim field3 As New field     'For Story's
      screen.MousePointer = 11    'To display the time to build
      Set newdb = CreateDatabase("NEWBTWDB.MDB", DB_LANG_GENERAL)
      newtd.Name = "Money_Table"  '* New table name
      field1.Name = "PK_ID"       '* Holds PK ID
      field1.Type = DB_LONG
      newtd.Fields.Append field1
      field2.Name = "Money"       '* Holds Money
      field2.Type = DB_CURRENCY
      newtd.Fields.Append field2
      field3.Name = "Story"       '* Holds Story
      field3.Type = DB_TEXT
      field3.Size = 154
      newtd.Fields.Append field3
      newidx.Name = "PK_ID_IDX"   '* You have to have an index
      newidx.Fields = "PK_ID"
      newidx.Primary = True
      newtd.Indexes.Append newidx
      newdb.TableDefs.Append newtd
      Set newtb = newdb.OpenTable("Money_Table")
      For i% = 1 To 10
         newtb.AddNew
         newtb("PK_ID") = PrimaryKeys(i%)      'place in field1
         newtb("Money") = Money(i%)            'place in field3
         newtb("Story") = Trim$(Strings(i%))   'place in field4
         newtb.Update                          'Saving to table
      Next i%
      newtb.Close                 '* Close DB's table
      newdb.Close                 '* Close DB
      screen.MousePointer = 0     'Set back to show done
   End Sub

</PRE><P><LI>Add the following code to the Command3 Click event procedure:
<P>
<PRE>   Sub Command3_Click ()
      Dim db As Database
      Dim t As Table
      Dim counter%
      Set db = OpenDatabase("NEWBTWDB.MDB")
      Set t = db.OpenTable("Money_Table")
      counter% = 1           'Start counter at Row=1
      Do Until t.EOF
         grid2.Col = 1
         grid2.Row = counter%
         grid2.Text = t(0)         'Load the PK ID
         grid2.Col = 2
         grid2.Row = counter%
         grid2.Text = t(1)         'Load the Money
         grid2.Col = 3
         grid2.Row = counter%
         grid2.Text = t(2)         'Load the Story
         counter% = counter% + 1
         t.MoveNext
      Loop
      t.Close
      db.Close
   End Sub

</PRE><P><LI>From the Run menu, choose Start (ALT, R, S), or press the F5 key to run
   the program. First, click the Command1 button. Next, click the Command2
   button. Then click the Command3 button. Compare the results.
<P></OL>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional reference words: 3.00<BR>
KBCategory: kbinterop kbprg kbcode<BR>
KBSubcategory: APrgDataIISAM<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  June 21, 1995</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
