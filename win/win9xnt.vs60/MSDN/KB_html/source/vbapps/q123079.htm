

<HTML>
<HEAD>
<TITLE>ACC: Sample Functions to Check User, Group Info </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q123079">
<META NAME="KBModify" CONTENT="1997/08/29">
<META NAME="KBCreate" CONTENT="1994/11/21">
<META NAME="Keywords" CONTENT="kbprg kbusage PgmHowTo">
<META NAME="KBArea" CONTENT="Support; KB; vbapps">
<META NAME="Description" CONTENT="  Advanced: Requires expert coding, interoperability, and multiuser skills.  This article contains several sample user-defined functions that you can use to do the following:   - Return a list of users in the current system database.  - Return a list...">
<META NAME="Product" CONTENT="vbapps">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QABO,QAY5,QAGU,QA1S,QAUD,QBV8,QBBI,QAB9,QDN9,QAB4,QAAP,QAUF,QAVX,QAGB,QBS0 P1 P2 P3 P4 P5 P6 T2 P7 V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>ACC: Sample Functions to Check User, Group Info</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  August 29, 1997</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q123079</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
 
The information in this article applies to:

<UL><LI>Microsoft Access versions 2.0, 7.0, 97
</UL> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
Advanced: Requires expert coding, interoperability, and multiuser skills.
<P>
This article contains several sample user-defined functions that you can
use to do the following:

<UL><LI>Return a list of users in the current system database.
<LI>Return a list of groups in the current system database.
<LI>Return a list of users in a specified group.
<LI>Return a list of groups to which a specified user belongs.
<LI>Determine if the current user belongs to a specified group.
<P>
</UL>This article assumes that you are familiar with Visual Basic for
Applications and with creating Microsoft Access applications using the
programming tools provided with Microsoft Access. For more information
about Visual Basic for Applications, please refer to your version of the
"Building Applications with Microsoft Access" manual.
<P>
NOTE: Visual Basic for Applications is called Access Basic in Microsoft
Access version 2.0. For more information about Access Basic, please refer
to the "Building Applications" manual.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
You can use the following sample functions to return user and group
information in the current system database. Note that each function assumes
there is a user called Developer who is a member of the Admins group, and
that the Developer account has no password.
<P>
<P><h3>The Sample Functions</h3>
 
<P>
<PRE>   '********************************************************
   ' Declarations section of the module
   '********************************************************

   Option Compare Database
   Option Explicit

   Function ListUsersInSystem ()
   '****************************************************************
   ' Purpose: Lists users in the current system database.
   ' Accepts: No arguments.
   ' Returns: A list of users in the current system database.
   ' Assumes: The existence of a user called Developer in the Admins
   '          group, with no password.
   '****************************************************************

   On Error GoTo err_ListUsersInSystem

   Dim MyWorkSpace As WorkSpace, i As Integer

   ' Create a new workspace as a member of the Admins group.
   Set MyWorkSpace = DBEngine.CreateWorkspace("SPECIAL", "Developer", "")

   For i = 0 To MyWorkSpace.Users.count - 1
        Debug.Print MyWorkSpace.Users(i).Name
   Next i

   MyWorkSpace.Close
   Exit Function

   err_ListUsersInSystem:
   If Err = 3029 Then
        MsgBox "The account used to create the workspace does not exist"
   Else MsgBox Error(Err)
   End If

   MyWorkSpace.Close
   Exit Function

   End Function

   Function ListGroupsInSystem ()
   '****************************************************************
   ' Purpose: Lists groups in the current system database.
   ' Accepts: No arguments.
   ' Returns: A list of groups in the current system database.
   ' Assumes: The existence of a user called Developer in the Admins
   '          group, with no password.
   '****************************************************************

   On Error GoTo err_ListGroupsInSystem

   Dim MyWorkSpace As WorkSpace, i As Integer

   ' Create a new workspace as a member of the Admins group.
   Set MyWorkSpace = DBEngine.CreateWorkspace("SPECIAL", "Developer", "")
   For i = 0 To MyWorkSpace.Groups.count - 1
        Debug.Print MyWorkSpace.Groups(i).Name
   Next i

   MyWorkSpace.Close

   Exit Function

   err_ListGroupsInSystem:
   If Err = 3029 Then
        MsgBox "The account used to create the workspace does not exist"
   Else MsgBox Error(Err)
   End If

   MyWorkSpace.Close
   Exit Function

   End Function

   Function ListUsersOfGroup (GroupName As String)
   '****************************************************************
   ' Purpose: Lists users who are members of the specified group in
   '          the current system database.
   ' Accepts: The name of a group.
   ' Returns: A list of users in the specified group.
   ' Assumes: The existence of a user called Developer in the Admins
   '          group, with no password.
   '****************************************************************

   On Error GoTo err_ListUsersOfGroup

   Dim MyWorkSpace As WorkSpace, i As Integer
   Dim MyGroup As Group

   ' Create a new workspace as a member of the Admins group.
   Set MyWorkSpace = DBEngine.CreateWorkspace("SPECIAL", "Developer", "")

   Set MyGroup = MyWorkSpace.Groups(GroupName)

   For i = 0 To MyGroup.Users.count - 1
        Debug.Print MyGroup.Users(i).Name
   Next i

   MyWorkSpace.Close
   Exit Function

   err_ListUsersOfGroup:
   If Err = 3265 Then
        MsgBox UCase(GroupName) &amp; " isn't a valid group name", 16, "Error"
   ElseIf Err = 3029 Then
        MsgBox "The account used to create the workspace does not exist"
   Else MsgBox Error(Err)
   End If

   MyWorkSpace.Close
   Exit Function

   End Function

   Function ListGroupsOfUser (UserName As String)
   '****************************************************************
   ' Purpose: Lists the groups to which a specified user belongs.
   ' Accepts: The name of a user.
   ' Returns: A list of groups for the specified user.
   ' Assumes: The existence of a user called Developer in the Admins
   '          group, with no password.
   '****************************************************************

   On Error GoTo err_ListGroupsOfUser

   Dim MyWorkSpace As WorkSpace, i As Integer
   Dim MyUser As User

   ' Create a new workspace as a member of the Admins group.
   Set MyWorkSpace = DBEngine.CreateWorkspace("SPECIAL", "Developer", "")

   Set MyUser = MyWorkSpace.Users(UserName)

   For i = 0 To MyUser.Groups.count - 1
        Debug.Print MyUser.Groups(i).Name
   Next i

   MyWorkSpace.Close
   Exit Function

   err_ListGroupsOfUser:
   If Err = 3265 Then
        MsgBox UCase(UserName) &amp; " isn't a valid user name", 16, "Error"
   ElseIf Err = 3029 Then
        MsgBox "The account used to create the workspace does not exist"
   Else MsgBox Error(Err)
   End If

   MyWorkSpace.Close
   Exit Function

   End Function

   Function CurrentUserInGroup (GroupName As String)
   '****************************************************************
   ' Purpose: Determines if the current user belongs to the specified
   '          group.
   ' Accepts: The name of a group.
   ' Returns: True if the current user is a member of the specified
   '          group, False if the current user is not a member of
   '          the group.
   ' Assumes: The existence of a user called Developer in the Admins
   '          group, with no password.
   '****************************************************************

   On Error GoTo err_CurrentUserInGroup

   Dim MyWorkSpace As WorkSpace, i As Integer
   Dim MyGroup As Group, MyUser As User

   ' Create a new workspace as a member of the Admins group.
   Set MyWorkSpace = DBEngine.CreateWorkspace("SPECIAL", "Developer", "")

   Set MyGroup = MyWorkSpace.Groups(GroupName)
   Set MyUser = MyWorkSpace.Users(CurrentUser())
   For i = 0 To MyGroup.Users.count - 1
        If MyGroup.Users(i).Name = MyUser.Name Then
             CurrentUserInGroup = True
             Exit Function
        End If
   Next i

   CurrentUserInGroup = False
   MyWorkSpace.Close
   Exit Function

   err_CurrentUserInGroup:
   If Err = 3265 Then
        MsgBox UCase(GroupName) &amp; " isn't a valid group name", 16, "Error"
        CurrentUserInGroup = False
   ElseIf Err = 3029 Then
        MsgBox "The account used to create the workspace does not exist"
   Else MsgBox Error(Err)
   End If

   MyWorkSpace.Close
   Exit Function

   End Function

</PRE>To test these functions, run them in the Debug window (or Immediate
window in Microsoft Access 2.0). For example, to test the
ListGroupsOfUser() function, follow these steps:

<OL><P><LI>Open the sample database Northwind.mdb (or NWIND.MDB in Microsoft Access
   2.0).

<P><LI>Create a new module and enter the sample functions above.

<P><LI>On the View menu, click Debug Window (or Immediate Window in Microsoft
   Access 2.0.)

<P><LI>In the Debug window, type the following line, and then press ENTER:
<P>
<P><PRE>      ? ListGroupsOfUser("Admin")
</PRE><P>
</OL><h2>REFERENCES</h2>
 
<P>
For more information about Users, search for "User Object," and then
"User Object" using the Microsoft Access Help Index.
<P>
For more information about Groups, search for "Group Object," and then
"Group Object" using the Microsoft Access Help Index.
<P>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional query words: security dao retrieve<BR>
Keywords          : kbprg kbusage PgmHowTo<BR>
Version           : 2.0 7.0 97<BR>
Platform          : WINDOWS<BR>
Hardware          : x86<BR>
Issue type        : kbhowto<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  August 29, 1997</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
