

<HTML>
<HEAD>
<TITLE>How to Change the Default Windows Printer from FoxPro </TITLE>

<!--STYLE_BEGIN-->
<style>@import url(../../msdn_ie4.css);</style>
	<link disabled rel="stylesheet" href="../../msdn_ie3.css">
<!--STYLE_END-->

<META HTTP-EQUIV="CONTENT-Type" CONTENT="text/html; charset=us-ascii">
<META NAME="ms.locale" CONTENT="EN-US">
<META NAME="Category" CONTENT="Support; KB Article">
<META NAME="KBID" CONTENT="Q103645">
<META NAME="KBModify" CONTENT="1998/02/23">
<META NAME="KBCreate" CONTENT="1993/08/26">
<META NAME="Keywords" CONTENT="FxprintDriver kbcode kbprint">
<META NAME="KBArea" CONTENT="Support; KB; foxpro, crossnet, odbc">
<META NAME="Description" CONTENT="  With the set of procedures included in this article, you can change the default Windows printer from within FoxPro for Windows.  NOTE: These procedures also work in Window NT. The method described below does not work with FoxPro 2.6a in Windows 95....">
<META NAME="Product" CONTENT="Visual FoxPro">
<META NAME="Platform" CONTENT="Windows">
<META NAME="Technology" CONTENT=" ">
<META NAME="Premium" CONTENT="support">

<META NAME="nyms" CONTENT="QAB5,QAKC,QAOX,QACT,QBC6,QAG8,QAHE,QAUD,QABA,QAB9,QAH6,QBXS,QAH7,QAD7,QDL9 V02180118">
</HEAD>

<BODY BGCOLOR="#FFFFFF" LEFTMARGIN="0" RIGHTMARGIN="0" TOPMARGIN="0">


<!--DOCBODY_START-->
<BR>
<CENTER>
<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0" VALIGN="top" WIDTH="90%">
	<TR>
		<TD VALIGN="top">
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<H1>How to Change the Default Windows Printer from FoxPro</H1>
			</FONT>
			<FONT FACE="verdana,arial,helvetica" SIZE="1">
			Last reviewed:  February 23, 1998</FONT><BR>
			<FONT FACE="verdana,arial,helvetica" SIZE="2">
			<B>Article ID: Q103645</B>
			</FONT>
		</TD>
	</TR>
	<TR>
	<TD>
	<FONT FACE="Verdana, Arial, Helvetica" SIZE=2>



 
2.5x 2.60 2.60a 3.00 5.00
WINDOWS
kbprint kbcode
<P>
 
The information in this article applies to:

<UL><LI>Microsoft Visual FoxPro for Windows, versions 3.0, 5.0
<LI>Microsoft FoxPro for Windows, versions 2.5x 2.6, 2.6a
</UL> 
<P>
<P><h2>SUMMARY</h2>
 
<P>
With the set of procedures included in this article, you can change the
default Windows printer from within FoxPro for Windows.
<P>
NOTE: These procedures also work in Window NT. The method described below
does not work with FoxPro 2.6a in Windows 95.
<P>
<P><h2>MORE INFORMATION</h2>
 
<P>
Following is a summary of the three main procedures: LISTPRINT, GETPRINT,
and PUTPRINT.
<P>
<P><h3>LISTPRINT</h3>
 
<P>
LISTPRINT displays on the screen all the printers in the WIN.INI file, the
number of printers, and the default printer.
<P>
<P><h3>GETPRINT</h3>
 
<P>
The GETPRINT procedure is called by LISTPRINT and gets a list of available
printers, the number of printers, and the default printer in the WIN.INI
file. The parameters are as follows:

<UL><LI>Printer list (array)
<LI>Number of printers in WIN.INI file (numeric)
<LI>Default printer in the WIN.INI file (character)
<P>
</UL><h3>PUTPRINT</h3>
 
<P>
The PUTPRINT procedure sets the default printer in the WIN.INI file. This
procedure takes three or four parameters, as follows

<UL><LI>Printer list (array--Use parameter 1 from GETPRINT.)
<LI>Number of printers (numeric--Use parameter 2 from GETPRINT.)
<LI>The new default print string (Put directly into the WIN.INI file.)
<P>
</UL>or passes two numeric parameters (3 and 4):

<UL><LI>Number of the printer in the printer array (numeric)
<LI>Port number (If a printer has more than one port [FILE:, LPT1:] the
   number will represent the number in the list order [FILE: = 1,
   LPT1: = 2]. If a third parameter is passed that is numeric, but no
   fourth parameter is passed, it will default to the first or only
   port.)
<P>
</UL>For example, after first executing the LISTPRINT procedure with the DO
LISTPRINT command, the WIN.INI file's printer information is placed in the
device array. The PUTPRINT procedure can then be used to change the default
printer driver. There are two ways to use the GETPRINT and PUTPRINT
routines, as shown in the examples below. (Example 1 is the easier of the
two). LISTPRINT is the only a way to display the current printers. Both
examples use GETPRINT to pass the number of printers back to PUTPRINT.
<P>
<P><h3>Example 1</h3>
 
<P>
<PRE>   Printers = ""
   Number = 0
   Defa_print = ""
   DO GETPRINT WITH Printers, Number, Defa_print
   *Printers   - is an array with all the printers installed.
   *Number     - is the number of printers in the WIN.INI file.
   *Defa_print - is the default printer in the WIN.INI file.

   DO PUTPRINT WITH Printers, Number, x
   *Printers   - is the return array from the GETPRINT.
   *Number     - is the return number of printers from GETPRINT.
   *x          - is the number of the row (in the Printers array) that
   *             you want to be the new default printer.
   *y          - is only needed if the printer has more than one output
   *             port.  If it does, you use this parameter to set the port
   *             (LPT1 = 1, LPT2 = 2 and LPT3 = 3)

</PRE><h3>Example 2</h3>
 
<P>
<PRE>   Printers = ""
   Number = 0
   Defa_print = ""
   DO GETPRINT WITH Printers, Number, Defa_print
   *Printers   - is an array with all the printers installed.
   *Number     - is the number of printers in the WIN.INI file.
   *Defa_print - is the default printer in the WIN.INI file.

   DO PUTPRINT WITH Printers, Number, NEW_DEFA
   *Printers   - is the return array from the GETPRINT.
   *Number     - is the return number of printers from GETPRINT.
   *NEW_DEFA   - is the new default string that will be put directly into
   *             the WIN.INI file (e.g., "HP LaserJet III,HPPCL5MS,LPT1;")
   *             This should only be done if you know exactly what should
   *             be in the Default printer string.

</PRE>Keep in mind that PUTPRINT puts the information that you supply directly
into the WIN.INI file. Therefore, GETPRINT is used first, so that no
installed printers are lost while you are setting a new default printer.
<P>
To verify that the correct driver has been set as the default, issue the
following commands:
<P>
<PRE>   RELEASE device
   DO LISTPRINT

</PRE>Following is the code for the three main procedures, GETPRINT, LISTPRINT,
and PUTPRINT, which make calls to three additional procedures, PUTPROSTRG,
GETPROSTRG, and GETPORT.
<P>
NOTE: Removing the SET TALK lines from this code will cause a "Cannot
access characters beyond string" error.
<P>
<PRE>   *!*****************************************************************
   *!
   *!     Procedure: GETPRINT
   *!
   *!*****************************************************************
   PROCEDURE getprint
   * Query WIN.INI to get data on the installed and default printers.

   PARAMETER DEVICE, NUMBER, dfltprnt
   * Usage:
   *    DIMENSION device[1]
   *    numprinters = 0
   *    defaultprnt = ""
   *    DO GETPRINT WITH device, numprinters, defaultprnt
   *
   * The "device" array will be populated with the names and
   * parameters for all installed print devices.
   *
   * The "device" array has this structure:
   * Col 1: Printer name
   * Col 2: Parameter for [device] section
   * Col 3: Parameter for [PrinterPort] section (includes time-out
   * parameters)
   *
   * The contents of "device" might look something like this after
   * GETPRINT is called:
   *   Col 1                      Col 2           Col 3
   *   -------------------------  --------------  ----------------
   *   Apple LaserWriter II NTX   pscript,LPT2:   pscript,LPT2:,15,90
   *   Generic / Text Only=TTY    fred.prn        fred.prn,15,45
   *   HP LaserJets(Level 5)      HPPCL5MS,LPT1:  HPPCL5MS,LPT1:,15,45
   *
   * "numprinters" in this case would be 3
   * "defaultprnt" might be this:
   *    HP LaserJets(Level 5),HPPCL5MS,LPT1:

   #DEFINE buflen 2048
   PRIVATE m.in_talk, m.dcount, m.retbuf, m.bytes, m.thisdevice

   IF PARAMETERS() &lt; 3
      WAIT WINDOW "This procedure requires 3 parameters"
      RETURN
   ENDIF

   IF FILE(SYS(2004)+"FOXTOOLS.FLL")
      SET LIBRARY TO (SYS(2004)+"foxtools.fll") ADDITIVE
   ELSE
      WAIT WINDOW "GETPRINT requires the FoxTools library."
      RETURN
   ENDIF

   IF SET("TALK") = "ON"
      SET TALK OFF
      m.in_talk = "ON"
   ELSE
      m.in_talk = "OFF"
   ENDIF

   * Fill in the first column of the array with installed device
   * names.
   m.retbuf = REPLICATE(CHR(0),buflen)
   m.bytes = getprostrg("devices",0,CHR(0),@m.retbuf,buflen)
   * The second argument of 0 to GetProfileString() returns the contents
   * of the entire section, with each entry separated by a null terminator
   * (CHR(0)).
   m.dcount = 0
   m.retbuf = LEFT(m.retbuf,m.bytes)
   DO WHILE CHR(0) $ m.retbuf
      m.thisdevice = LEFT(m.retbuf,AT(CHR(0),m.retbuf)-1)
      IF LEFT(m.thisdevice,1) &lt;&gt; CHR(0)
         m.dcount = m.dcount + 1
         DIMENSION device[m.dcount,3]
         device[m.dcount,1] = m.thisdevice
      ENDIF
      m.retbuf = SUBSTR(m.retbuf,AT(CHR(0),m.retbuf)+1)
   ENDDO

   * Fill in the second and third columns of the device array with the
   * parameters of each installed device from the [devices] section
   * (column 2) and the [PrinterPorts] section (column 3).
   FOR m.j = 1 TO m.dcount
      retbuf = REPLICATE(CHR(0),256)
      m.bytes = ;
         getprostrg("devices",device[m.j,1],CHR(0),@m.retbuf,256)
      m.retbuf = LEFT(m.retbuf,m.bytes)
      device[m.j,2] = m.retbuf

      retbuf = REPLICATE(CHR(0),256)
      m.bytes = ;
         getprostrg("PrinterPorts",device[m.j,1],CHR(0),@m.retbuf,256)
      m.retbuf = LEFT(m.retbuf,m.bytes)
      device[m.j,3] = m.retbuf
   ENDFOR

   * Store the number of installed devices.
   m.number = m.dcount

   * Now get the default printer.
   retbuf = REPLICATE(CHR(0),256)
   m.bytes = getprostrg("windows","device",CHR(0),@m.retbuf,256)
   m.retbuf = LEFT(m.retbuf,m.bytes)
   m.dfltprnt = m.retbuf

   SET TALK &amp;in_talk

   *!*****************************************************************
   *!
   *!     Procedure: LISTPRINT
   *!
   *!*****************************************************************
   PROCEDURE listprint
   PARAMETER DEVICE
   #DEFINE buflen 2048
   PRIVATE m.in_talk,m.i
   IF FILE(SYS(2004)+"FOXTOOLS.FLL")
      SET LIBRARY TO (SYS(2004)+"foxtools.fll") ADDITIVE
   ELSE
      WAIT WINDOW "LISTPRINT requires the FoxTools library."
      RETURN
   ENDIF
   IF SET("TALK") = "ON"
      SET TALK OFF
      m.in_talk = "ON"
   ELSE
      m.in_talk = "OFF"
   ENDIF

   DIMENSION DEVICE(1)
   NUMBER = 0
   dflt = ""

   DO getprint WITH DEVICE, NUMBER, dflt
   CLEAR
   ? ALLTRIM(STR(m.number,3))+" installed printer devices in win.ini:"
   FOR m.i = 1 TO NUMBER
      ? "  "+device[m.i,1]+"="+device[m.i,3]
   ENDFOR
   ?
   ? "Default printer is "+dflt
   SET TALK &amp;in_talk

   *!*****************************************************************
   *!
   *!     Procedure: PUTPRINT
   *!
   *!*****************************************************************
   PROCEDURE putprint
   PARAMETER DEVICE, NUMBER, dflt, portnum
   * Take the printer device information in the device array and
   * update WIN.INI with it.
   *
   * "Number" is the total number of printers listed in "device."
   * "dflt" is the string to write to the [windows] device= statement
   * to set the default printer, or if dflt is a number, the array row
   * to construct this statement from. Thus, to make the default
   * printer the third one listed in "device," either pass a 3 as dflt
   * or pass the actual string (something like HP LaserJets(Level
   * 5),HPPCL5MS,LPT1:). If the string is passed, it must exactly
   * match an entry in the [devices] section of the WIN.INI file.
   *
   * GETPRINT can be used to construct the device array.
   *
   IF FILE(SYS(2004)+"FOXTOOLS.FLL")
      SET LIBRARY TO (SYS(2004)+"foxtools.fll") ADDITIVE
   ELSE
      WAIT WINDOW "PUTPRINT requires the FoxTools library."
      RETURN
   ENDIF

   IF TYPE("dflt") = "N"
      IF PARAMETERS() &lt; 4
         portnum = 1
      ENDIF
      m.strg = device[m.dflt,1]+","+getport(device[m.dflt,2],portnum)
   ELSE
      m.strg = m.dflt
   ENDIF

   * Set the default printer
   =putprostrg("windows","device",m.strg)

   * Delete all existing device and PrinterPort entries.
   =putprostrg("devices",0,CHR(0))
   =putprostrg("PrinterPorts",0,CHR(0))

   FOR m.i = 1 TO NUMBER
      =putprostrg("devices",device[m.i,1],device[m.i,2])
      =putprostrg("PrinterPorts",device[m.i,1],device[m.i,3])
   ENDFOR

   *!*****************************************************************
   *!
   *!     Function: PUTPROSTRG
   *!
   *!*****************************************************************
   FUNCTION putprostrg
   PARAMETER SECTION, entry, string
   fn = regfn("WRITEPROFILESTRING","CCC","I")
   RETURN callfn(fn,SECTION,entry,string)
   * NOTE: When the code in this section is implemented in a program
   * running on Windows 95 or Windows NT, the user will get the error
   * "Entry point writeprofilestring not found." In this case, add the
   * optional fourth argument to the call to function call RegFN()
   * in the function named putprostrg as per the following example:
   *
   * fn=regfn("WRITEPROFILESTRING","CCC","I","krnl386.exe")
   *
   * The string "krnl386.exe" specifies that the writeprofilestring API
   * is located in that library.
   *!*****************************************************************
   *!
   *!     Function: GETPROSTRG
   *!
   *!*****************************************************************
   FUNCTION getprostrg
   PARAMETER SECTION, entry, dflt, buffer, blen
   fn = regfn("GETPROFILESTRING","CCC@CI","I")
   RETURN callfn(fn,SECTION,entry,dflt,@buffer,blen)
   * NOTE: When the code in this section is implemented in a program
   * running on Windows 95 or Windows NT, the user will get the error
   * "Entry point getprofilestring not found." In this case, add the
   * optional fourth argument to the call to function call RegFN()
   * in the function named Getprostrg as per the following example:
   *
   * fn=RegFN("GETPROFILESTRING","CCC@CI","I","krnl386.exe")
   *
   * The string "krnl386.exe" specifies that the getprofilestring API
   * is located in that library.

   *!*****************************************************************
   *!
   *!     Function: GETPORT
   *!
   *!*****************************************************************
   FUNCTION getport
   PARAMETER m.pstrg, m.pnum
   * Get the first "port" from a printer string.

   * First get the printer driver name (e.g., pscript) and the comma.
   m.retstrg = SUBSTR(m.pstrg,1,AT(',',m.pstrg))

   * Now get the port designation (e.g., LPT1: or FILE:)

   * Check if the port passed is greater than the ports available.
   IF OCCURS(',',m.pstrg) &gt;= m.pnum
      m.portstrg = SUBSTR(m.pstrg,AT(',',m.pstrg,m.pnum)+1)
   ELSE
      m.portstrg = SUBSTR(m.pstrg,AT(',',m.pstrg,1)+1)
   ENDIF

   IF AT(',',m.portstrg) &gt; 0
      m.portstrg = LEFT(m.portstrg,AT(',',m.portstrg)-1)
   ENDIF

   RETURN m.retstrg + m.portstrg
</PRE>

	</FONT>
	</TD>
	</TR>
</TABLE>
<P>


<!--DOCBODY_END-->

<!--FOOTER_START-->
<table cellpadding=5 border=0 width="90%">
<tr>
<td>
	<HR WIDTH="90%">

<BLOCKQUOTE>
<FONT FACE="Verdana, Arial, Helvetica" SIZE="1">
<SPAN STYLE="font-family:verdana,arial,helvetica; font-size:8pt">
Additional reference words: 5.00 VFoxWin 3.00 FoxWin 2.50 2.50a 2.50b 2.60<BR>
2.60a print program select pick choose programmatically control panel on<BR>
the fly default printer<BR>
KBCategory: kbprint kbcode<BR>
KBSubcategory: FxprintDriver<BR>
Keywords          : FxprintDriver kbcode kbprint<BR>
Version           : WINDOWS:2.5x,2.6,2.6a,3.0,5.0<BR>
Platform          : WINDOWS<BR>

</SPAN>
</FONT>
<BR>
<BR>
THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
SO THE FOREGOING LIMITATION MAY NOT APPLY.
</BLOCKQUOTE>
<P>
<center>
	<FONT FACE="Verdana,Arial,Helvetica" SIZE="1">
	Last reviewed:  February 23, 1998</FONT>
	<BR>
	<A href="../cpyright.htm" STYLE="font: bold 7pt Verdana,Arial,Helvetica">&copy; 1998 Microsoft Corporation. All rights reserved. Terms of Use.</A><BR>
	
</center>
</td>
</tr>
</table>

<!--FOOTER_END-->
</FONT>
</BODY>
</HTML>
